###############################################################################
# bazaar.des - Bazaar entry vaults and bazaar layouts.
#
# Bazaars should in general not contain dangers. Players expect that bazaars
# are peaceful. Challenges can come up, but those bazaars are weighted to be
# especially rare. Apart from this, plain and small maps are weighted to be
# generic (i.e. common), with the weirder or larger ones providing surprises.
###############################################################################


###############################################################################
# Bazaar entries

# Utility functions

lua {{

function place_bazaar_portal(e, perm)
  e.tags("bzr_entry")
  e.kfeat("O = enter_bazaar")
  if perm then
     e.lua_marker('O', bazaar_portal(false))
  else
     e.lua_marker('O', bazaar_portal(false))
  end
end

function bazaar_portal(timed)
  local messager =
    timed_msg {
      initmsg = { "You hear coins being counted.",
                  "An interdimensional caravan has stopped on this level "
                  .. "and set up a bazaar. Hurry and find its entrance before "
                  .. "they move on!" },
      finalmsg = "You hear the last, dying notes of the bell.",
      verb = 'tolling',
      noisemaker = 'bell'
    }
  local blow = 200 + 30*(40 - you.absdepth()) + crawl.random2(200)
  local bhigh = blow + crawl.random2(blow) + 100
  if timed then
    return timed_marker {
                  low=blow, high=bhigh, msg=messager, single_timed=true,
                  disappear='The gate to the bazaar disappears!',
                  desc = "flickering gateway to a bazaar",
                  floor = 'stone_arch', feat_tile = 'dngn_portal_bazaar_gone'
              }
  else
    return one_way_stair { floor = 'stone_arch' }
  end
end

function random_bazaar_colour()
  local colours = {"blue", "red", "lightblue", "magenta", "green"}
  local ret = colours[crawl.random2(#colours) + 1]

  return ret
end

function bazaar_setup(e)
  e.tags('no_monster_gen')
  e.kfeat("< = stone_arch")
  e.kfeat("> = exit_bazaar")
end

function fixup_bazaar()
  dgn.change_floor_colour(random_bazaar_colour())

  local default = {rock="wall_vault", floor="floor_vault", halo="halo_vault"}
  local tileset = {
    blue = {rock="wall_brick_gray", floor="floor_grass", halo="halo_grass"},
    red = {rock="wall_pebble_red", floor="floor_vault", halo="halo_vault"},
    lightblue = {rock="wall_pebble_lightblue", floor="floor_grass", halo="halo_grass2"},
    magenta = {rock="wall_stone_gray", floor="floor_dirt", halo="halo_dirt"},
    green = {rock="wall_stone_gray", floor="floor_grass", halo="halo_grass"},
  }

  local tile = tileset[dgn.get_floor_colour()]
  if (tile == nil) then
    tile = default
  end

  dgn.change_floor_tile(tile.floor)
  dgn.change_rock_tile(tile.rock)
end
  
dgn.set_branch_epilogue("Bazaar", "fixup_bazaar")

}}

default-depth: D:11-, Depths, Orc:1-2, Snake, Shoals, Vaults:1-2

###############################################################################
# Entries.
# The chance for a bazaar entry is branch and absdepth dependent,
# and resides in des/builder/shops.des.

NAME:   bzr_entry_dummy
TAGS:   transparent allow_dup
veto {{ return you.is_level_on_stack("Bazaar") }}
: place_bazaar_portal(_G)
MAP
O
ENDMAP

# A simple water entry.
NAME:    bzr_entry_water
TAGS:    no_pool_fixup
SHUFFLE: wwl
: place_bazaar_portal(_G)
MAP
 www
w.w.w
wwOww
w.w.w
 www
ENDMAP

# Some small bazaar entries
NAME: bzr_entry_doored
: place_bazaar_portal(_G)
MAP
xx+xx
x...x
+.O.+
x...x
xx+xx
ENDMAP

NAME: bzr_entry_hallway
: place_bazaar_portal(_G)
MAP
xxxxx
x.O.x
x...x
x...x
x...x
x...x
x...x
x.G.x
x...x
x...x
x...x
x...x
x...x
ENDMAP

NAME: bzr_entry_grove
: place_bazaar_portal(_G)
MAP
t..t..t
.......
..t.t..
t..O..t
..t.t..
.......
t..t..t
ENDMAP

NAME: bzr_entry_arches
: place_bazaar_portal(_G)
MAP
cccccccc
+A..A..c
ccccccAc
cO..A..c
cccccccc
ENDMAP

NAME: bzr_entry_dried
: place_bazaar_portal(_G)
MAP
.V.
VOV
.V.
ENDMAP

NAME: bzr_entry_wishing
KFEAT: C = altar_gozag
KITEM: T = nothing / q:1 gold
: place_bazaar_portal(_G)
MAP
   T
  ...
 .....
T..O..T
 .....
  ...
   C
ENDMAP
   
NAME: bzr_entry_metal_box
NSUBST: x = 1:+ / *:v
: place_bazaar_portal(_G)
MAP
xxx
xOx
xxx
ENDMAP

NAME: bzr_entry_crystal
: place_bazaar_portal(_G)
MAP
  .b.
 .....
.......
b..O..b
.......
 .....
  .b.
ENDMAP

NAME: bzr_entry_room
: place_bazaar_portal(_G)
MAP
c++ccccc++c
c.........c
c.........c
c..G...G..c
c.........c
c....O....c
ccccccccccc
ENDMAP

NAME: bzr_entry_crossroad
: place_bazaar_portal(_G)
MAP
   x.@.x
   x...x
   x...x
xxxx...xxxx
...........
@....O....@
...........
xxxx...xxxx
   x...x
   x...x
   x.@.x
ENDMAP

# Some coins to shop with.
NAME:   bzr_entry_gold
SUBST:  $=$.
: place_bazaar_portal(_G)
MAP
xx.xx
x$$$x
.$O$.
x$$$x
xx.xx
ENDMAP

# Many customers
NAME:    bzr_entry_customers
MONS:    human, orc, goblin, kobold
SUBST:   . = .:150 1
SHUFFLE: 1234
: place_bazaar_portal(_G)
MAP
 .....
.......
...O...
.......
 .....
ENDMAP

# Safe
NAME:   bzr_entry_safe
: place_bazaar_portal(_G)
MAP
 xxxxx
xx...xx
+..O..x
xx...xx
 xxxxx
ENDMAP

# Safe 2
NAME:    bzr_entry_islet
SHUFFLE: wwlW
: place_bazaar_portal(_G)
MAP
 wwwww
wwwwwww
ww<O>ww
wwwwwww
 wwwww
ENDMAP

# Portal along the road
NAME:    bzr_entry_road
SHUFFLE: XU, TUV
SUBST:   X=.
: place_bazaar_portal(_G)
MAP
   xxx
  xxOxx
xxx.X.xxx
@U.....U@
xxxxxxxxx
ENDMAP

# Portal by the lakeside (jpeg)
NAME:    bzr_entry_lakeside
TAGS:    no_rotate
MONS:    plant
SHUFFLE: XY
SUBST:   a = W .
SUBST:   X : T 1
SUBST:   Y = .
: place_bazaar_portal(_G)
MAP
     waWa.
   wwwWa.X.Y.
  wwwWO..Y.X.
wwwwwwaaaX.Y.
   wwwwwWa.
ENDMAP

# Bazaar entry with statue (Lemuel)
NAME:   statue_bzr
KPROP:  O = no_tele_into
SUBST:  G = G111
MONS:   obsidian statue / ice statue
: place_bazaar_portal(_G, true)
MAP
 @
cGc
cOc
ccc
ENDMAP

# Freezing bazaar (Lemuel)
NAME:   freezing_bzr
MARKER: W = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 1, pow_max = 5, delay_min = 20, delay_max = 30, \
             size = 2, walk_dist = 3, spread_rate= 20 }
: place_bazaar_portal(_G)
MAP
  W
 WWW
WWOWW
 WWW
  W
ENDMAP

# Bazaar in the mist (Lemuel)
NAME:   bzr_in_the_mist
MARKER: O = lua:fog_machine { cloud_type= "thin mist", \
            pow_min = 6, pow_max = 12, delay_min = 15, delay_max = 35, \
            size = 2, walk_dist = 2, spread_rate= 35 }
: place_bazaar_portal(_G)
MAP
O
ENDMAP

# Firewalk (Lemuel)
NAME:    bzr_firewalk
TAGS:    uniq_firewalk no_item_gen
SUBST:   $ : $ .
MARKER:  * = lua:fog_machine { cloud_type = "flame", \
             pow_min = 2, pow_max = 4, delay_min = 5, delay_max = 10, \
             size = 2, walk_dist = 0, spread_rate= 0 }
SUBST:   * = .
: place_bazaar_portal(_G)
MAP
c.@.$.@.c
c.*...*.c
c...$...c
c.......c
c.*.$.*.c
c.......c
c...$...c
c.*...*.c
c...O...c
ccccccccc
ENDMAP

# guarded
NAME: bzr_guarded
WEIGHT: 1
: place_bazaar_portal(_G)
MAP
  mmmmmmm
 mm..O..mm
mm...8...mm
 mm.....mm
  mm===mm
ENDMAP

#########################################
# Crystal gate, Pan only
# No allow_dup to prevent scumming.
NAME:    bzr_crystal_pan
TAGS:    extra
DEPTH:   Pan
COLOUR:  b = lightmagenta
COLOUR:  . = magenta
FTILE:   b.O = floor_crystal_squares
RTILE:   b = dngn_crystal_lightmagenta
: place_bazaar_portal(_G)
: set_feature_name("crystal_wall", "wall of crystal")
MAP
bb@bb
b...b
@.O.@
b...b
bb@bb
ENDMAP

###############################################################################
# Bazaar layouts.
#
# "encompass" levels are recommended, and can be as small or large as you like.
# No monsters are pre-placed in bazaars, and monsters do not spawn in bazaars,
# but you can place monsters in your maps if you know what you're doing.
#
# Every encompass bazaar level must have at least one downstair, which will be
# replaced with an exit from the bazaar. It's a good idea to also provide an
# upstair, which will be converted into a stone arch (and on which the player
# will be placed when entering the bazaar). If there's no upstair, the player
# will arrive on a random square.
#
# You can force a particular colour for the rock walls or floor using
# LROCKCOL and LFLOORCOL directives.
#
# Maps are sorted by WEIGHTs: common (20), rare (5), very rare (1).

default-depth: Bazaar

##########################################################################
# Common maps (weight 200)
##########################################################################

##########################################
# bazaar lakeside (jpeg)
# 5 shops
NAME:    bazaar_lake
TAGS:    no_rotate bazaar
ORIENT:  encompass
WEIGHT:  20
SUBST:   a = W.
SHUFFLE: ABCDEF
SUBST:   A = Y
SHUFFLE: XY
SUBST:   X = <, Y = >
# high quality shops, pity they don't stay :p
KFEAT:   B = general shop
KFEAT:   C = gadget shop / jewellery shop
KFEAT:   D = antique weapon shop
KFEAT:   E = antique armour shop
KFEAT:   F = scroll shop / distillery shop
# special cases for blue floor
: local colour = random_bazaar_colour()
: lfloorcol(colour)
: if colour == "blue" then
SUBST:   w = W
: end
#
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxx.....X.....xxxxxxxxxxxxxxx
xxxxxxxxxxx.................xxxxxxxxxxxx
xxxxxxxx.......................xxxxxxxxx
xxxxxx......T......F......T......xxxxxxx
xxxxx.............................xxxxxx
xxxx...............................xxxxx
xxx..............aaaaa..............xxxx
xxx.....E.......awwwwwaa......A.....xxxx
xx............aawwwwwwwww............xxx
xx...........awwwwwWwwwwa............xxx
xx..........awwwwwWWWwwwa............xxx
xxY.T..{.....awwwwwwWWwwwa........T.Yxxx
xx............awwwwwwwwaX............xxx
xx.............aawwwwwwwwa...........xxx
xx...............aaaaaaaa............xxx
xxx.....D.....................B.....xxxx
xxx.................................xxxx
xxxx...............................xxxxx
xxxxx.............................xxxxxx
xxxxxx......T......C......T......xxxxxxx
xxxxxxxx.......................xxxxxxxxx
xxxxxxxxxxx.................xxxxxxxxxxxx
xxxxxxxxxxxxxx.....X.....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

########################################
# bazaars in a row
# ~4 shops
NAME:    bazaar_short_row
TAGS:    no_rotate no_pool_fixup bazaar
ORIENT:  encompass
WEIGHT:  20
SHUFFLE: Aa/Bb/Cc/Dd/Ee/Ff
SHUFFLE: Aa/Zz, Bb/Yy, Cc/Rr, Dd/Ss
# two guaranteed shops, expected four shops
KFEAT:   A = any shop / antique weapon shop
KFEAT:   B = any shop / antique armour shop
KFEAT:   C = any shop / gadget shop
KFEAT:   D = any shop / jewellery shop
KFEAT:   E = any shop / weapon shop / armour shop
KFEAT:   F = any shop / book shop / scroll shop
SUBST:   b=a, c=a, d=a, e=a, f=a, a=T
SUBST:   y=z, r=z, s=z, z=V, Y=Z, R=Z, S=Z
KFEAT:   Z = abandoned_shop
SHUFFLE: lw
# special cases for blue/red floor
: local colour = random_bazaar_colour()
: lfloorcol(colour)
: if colour == "red" then
SUBST: l = w
: else
: if colour == "blue" then
SUBST: w = l
: end
: end
#
SUBST:   w:wWx, l:lx
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxAxxxxxxxxxxxCxxxxxxxxxxxExxxxxxxxxxxxxxxx
xxx.....xxxxxxx...xxxxxxxxx...xxxxxxxxx...xxxxxxx.....xxx
xx..www..xxxxxx.a.xxxxxxxxx.c.xxxxxxxxx.e.xxxxxx..lll..xx
x{.wwwww..+...........+...........+...........+..lllll.>x
xx..www..xxxxxx.b.xxxxxxxxx.d.xxxxxxxxx.f.xxxxxx..lll..xx
xxx.....xxxxxxx...xxxxxxxxx...xxxxxxxxx...xxxxxxx.....xxx
xxxxxxxxxxxxxxxxBxxxxxxxxxxxDxxxxxxxxxxxFxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    bazaar_jpeg_circle_common
WEIGHT:  20
TAGS:    no_rotate bazaar
ORIENT:  encompass
KFEAT:   A = any shop / antique armour shop
KFEAT:   B = any shop / antique weapon shop
KFEAT:   C = any shop / jewellery shop
KFEAT:   D = any shop / book shop
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx...xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx.....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx..D..xxxxxxxxxxxxxxx
xxxxxxxxxx....+.....+....xxxxxxxxxx
xxxxxxx....xxxxx...xxxxx....xxxxxxx
xxxxxx..xxxxxxxxx.xxxxxxxxx..xxxxxx
xxxxx..xxxxxxxxxx>xxxxxxxxxx..xxxxx
xxxxx+xxxxxxxxxx...xxxxxxxxxx+xxxxx
xxx...xxxxxxxx.......xxxxxxxx...xxx
xx.....xxxxxx.........xxxxxx.....xx
xx..A.......T....{....T.......B..xx
xx.....xxxxxx.........xxxxxx.....xx
xxx...xxxxxxxx.......xxxxxxxx...xxx
xxxxx+xxxxxxxxxx...xxxxxxxxxx+xxxxx
xxxxx..xxxxxxxxxx.xxxxxxxxxx..xxxxx
xxxxxx..xxxxxxxxx.xxxxxxxxx..xxxxxx
xxxxxxx....xxxxx...xxxxx....xxxxxxx
xxxxxxxxxx....+.....+....xxxxxxxxxx
xxxxxxxxxxxxxxx..C..xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx.....xxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxx...xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 2 good shops or 3 shops
NAME:    bazaar_jpeg_cross
TAGS:    no_rotate bazaar
WEIGHT:  20
ORIENT:  encompass
SHUFFLE: ABCD
: if crawl.coinflip() then
SUBST:   A = {, B = >
KFEAT:   C = jewellery shop / gadget shop
KFEAT:   D = antique armour shop / book shop
: else
SUBST:   T = {, A = >
KFEAT:   B = antique weapon shop
KFEAT:   C = antique armour shop
KFEAT:   D = general shop / distillery shop
:end
: if crawl.coinflip() then
SUBST:   ' = .
SUBST:   G : G.
: else
SUBST:   ' = x
SUBST:   G = .
:end
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxx
xxxxxxxxxxxx.......xxxxxxxxxxxx
xxxxxxxxxxx....A....xxxxxxxxxxx
xxxxxxxxxxxx.......xxxxxxxxxxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxx
xxxx...xxxxxxx...xxxxxxx...xxxx
xx......xxxx''.G.''xxxx......xx
x....B.......G.T.G.......D....x
xx......xxxx''.G.''xxxx......xx
xxxx...xxxxxxx...xxxxxxx...xxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxx
xxxxxxxxxxxx.......xxxxxxxxxxxx
xxxxxxxxxxx....C....xxxxxxxxxxx
xxxxxxxxxxxx.......xxxxxxxxxxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:      bazaar_minmay_generic_island
TAGS:      no_rotate bazaar
ORIENT:    encompass
WEIGHT:    20
SUBST:     w : wl
SHUFFLE:   Aa/Bb/Cc/Dd, Cc/Ee, Dd/Ff
COLOUR:    EeFf = yellow
SUBST:     ab = !, AB = ., cd = !, CD = ., EeFf = $
KFEAT:     ! = any shop
KFEAT:     ? = any shop / T
LFLOORCOL: blue
:          bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxx
xxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxx
xxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxx
xxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxx
xxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxx
xxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxx
xxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxx
xxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxx
xxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxx
xxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxx
xwwwwwwwwwwwwwwwwwwnnnnnnnnnwwwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwnn.......nnwwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwnn....!....nnwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwnn...........nnwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwnn..AAA...BBB..nnwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwn...AaA...BbB...nwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwn...AAA...BBB...nwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwn.............>.nwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwn.{.............nwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwn...CCC...DDD...nwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwn...CcC...DdD...nwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwnn..CCC...DDD..nnwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwnn...........nnwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwnn....!....nnwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwnn.......nnwwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwwnnnnnnnnnwwwwwwwwwwwwwwwwwwx
xxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxx
xxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxx
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxx
xxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxx
xxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxx
xxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxx
xxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxx
xxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxx
xxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxx
xxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxx
xxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    jpeg_bazaar_island
TAGS:    no_rotate no_pool_fixup bazaar
ORIENT:  encompass
WEIGHT:  20
SUBST:   W = W:30 w, ~ = ':20 W
NSUBST:  ' = 5:t / 7:1 / 4:2 / *:'
NSUBST:  A = 1:{ / *:'
NSUBST:  ! = 4:! / *:.
KFEAT:   ! = any shop
COLOUR:  ' = green / brown w:2
FTILE:   'A12 = floor_grass
FTILE:   .!T = floor_vault
: set_border_fill_type("open_sea")
:        bazaar_setup(_G)
MAP
wwwwwwwwWWWWWWWWWWWWWWWWWWWwwwwwwww
wwwwwwwWW~~~~~~~~~~~~~~~~~WWwwwwwww
wwwwwwWW~'''''''''''''''''~WWwwwwww
wwwwwWW~'''''''''''''''''''~WWwwwww
wwwwWW~'''''''''''''''''''''~WWwwww
wwwWW~''''''''ccccccc''''A'''~WWwww
wwWW~'''''''ccc.....ccc'''''''~WWww
wWW~''''''ccc...!.....ccc''''''~WWw
wW~''''''cc.........!...cc''''''~Ww
wW~''''''+...!........!..c''''''~Ww
wW~''A'''+.......!.T.....c'''>''~Ww
wW~''''''+...!........!..c''''''~Ww
wW~''''''cc.........!...cc''''''~Ww
wWW~''''''ccc...!.....ccc''''''~WWw
wwWW~'''''''ccc.....ccc'''''''~WWww
wwwWW~''''''''ccccccc''''A'''~WWwww
wwwwWW~'''''''''''''''''''''~WWwwww
wwwwwWW~'''''''''''''''''''~WWwwwww
wwwwwwWW~'''''''''''''''''~WWwwwwww
wwwwwwwWW~~~~~~~~~~~~~~~~~WWwwwwwww
wwwwwwwwWWWWWWWWWWWWWWWWWWWwwwwwwww
ENDMAP

NAME:    cyrus_bazaar_forest
TAGS:    no_rotate bazaar
ORIENT:  encompass
WEIGHT:  20
MONS:    plant, bush
NSUBST:  . = 10:X / 34:Y / *:.
NSUBST:  ! = 4:! / *:.
SUBST:   X : tXY., X = t., Y : Y., Y = 1112..
SUBST:   - = ., " : """.
KFEAT:   ! = any shop
COLOUR:  .12 = green
COLOUR:  " : brown
COLOUR:  ' : white
FTILE:   T.t12 = floor_grass
FTILE:   " = floor_dirt
FTILE:   '{+G! = floor_vault
:        bazaar_setup(_G)
MAP
tttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttt
tt..........................tt
tt.."""................"""..tt
tt.."!"...""".."""....."!"..tt
tt.."""..."!".."!"....."""..tt
tt........""".."""..........tt
tt..........................tt
tt--..""".."""..""".."""....tt
tt{---"!".."T".."T".."!"...>tt
tt--..""".."""..""".."""....tt
tt..........................tt
tt..........""".."""........tt
tt.."""....."!".."!"..."""..tt
tt.."!".....""".."""..."!"..tt
tt.."""................"""..tt
tt..........................tt
tttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttt
ENDMAP

NAME:   dpeg_three_leaves_bazaar
TAGS:   no_rotate bazaar
ORIENT: encompass
WEIGHT: 20
NSUBST: ! = 4:! / *:.
KFEAT:  ! = any shop
:        bazaar_setup(_G)
MAP
      bbbb               bbbb
    bbbb.bb             bb.bbbb
    bb....bb           bb....bb
   bb......bbb       bbb......bb
  bb.....!...bb     bb...!.....bb
 bb..!........bbb bbb........!..bb
 bb.............bbb.............bb
bb....!....T.....b.....T....!....bb
bb...............................bb
 bbbbbbbb........{........bbbbbbbb
        bbbbbbbb...bbbbbbbb
              bb...bb
             bb.....bb
            bb...T...bb
           bb.........bb
          bb...........bb
         bb...!..>..!...bb
        bb...............bb
        bbbb.....!.....bbbb
           bbbb.....bbbb
             bbbbbbbbb
ENDMAP

NAME:   zaba_magenta_bazaar
TAGS:   no_rotate bazaar
ORIENT: encompass
NSUBST: ! = 2:! / *:.
KFEAT:  ! = any shop
KFEAT:  B = distillery shop
COLOUR: c = magenta
:        bazaar_setup(_G)
MAP
ccccccccccccccccccccccccccccccccccccccccccc
cccc.......ccccccc.......ccccccc.......cccc
cc...!...!...ccc...T...T...ccc...!...!...cc
c.............c.............c.............c
cc.......................................cc
cccc>..B.............{.............B....ccc
cc.......................................cc
c.............c.............c.............c
cc...!...!...ccc...T...T...ccc...!...!...cc
cccc.......ccccccc.......ccccccc.......cccc
ccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

NAME:    bazaar_minmay_row
TAGS:    no_rotate bazaar
ORIENT:  encompass
WEIGHT:  20
SHUFFLE: T? / AB
SUBST:   A = !, B = T
KFEAT:   ! = any shop
KFEAT:   ? = any shop / T
SUBST:   T : Tc
:        bazaar_setup(_G)
MAP
       xxxxx
       x...x
       x.{.x
    xxxx...xxxx
  xxx.........xxx
 xx......c......xx
 x...............x
xx..c.........c..xx
x.................x
x.T.?.!.?.?.!.?.T.x
x.................x
xx..c.........c..xx
 x...............x
 xx......c......xx
  xxx.........xxx
    xxxx...xxxx
       x.>.x
       x...x
       xxxxx
ENDMAP

NAME:    nicolae_bazaar_no_exploring_needed
TAGS:    no_pool_fixup bazaar
ORIENT:  encompass
WEIGHT:  20
NSUBST:  ! = 4:! / *:T
KFEAT:   ! = any shop
:        bazaar_setup(_G)
MAP
   ccccccccc
  cc!.!.!.!cc
 cc.........cc
cc!.........!cc
c.............c
c!...........!c
c......>......c
c!.....{.....!c
c.............c
c!...........!c
c.............c
cc!.........!cc
 cc.........cc
  cc!.!.!.!cc
   ccccccccc
ENDMAP

NAME:    bazaar_eino_lava
TAGS:    no_rotate bazaar
ORIENT:  encompass
COLOUR:  c = red
KFEAT:   B = any shop
:        bazaar_setup(_G)
MAP
      ccc     ccc
     cclccccccclcc
  ccccl.lcclccl.lcccc
 cclll...c...c...lllcc
 clll....G...G....lllc
 cll.......B.......llc
 cl.................lc
 ccG...............Gccc
cc.........>.........cc
cl..B......{......B..lc
cc...................cc
 ccG...............Gcc
 cl.................lc
 cll.......B.......llc
 clll....G...G....lllc
 cclll...c...c...lllcc
  ccccl.lcclccl.lcccc
     cclccccccclcc
      ccc     ccc
ENDMAP

NAME:    bazaar_hellmonk_w
TAGS:    no_rotate no_pool_fixup bazaar
ORIENT:  encompass
SHUFFLE: AB
SUBST:   A = {, B = >
KFEAT:   C = any shop
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx......................xxx
xxW..........WW..........Wxx
xWWW..C..C..WWWW..C..C..WWWx
x.WWW......WWWWWW......WWW.x
x..WWW....WWW..WWW....WWW..x
x...WWW..WWW....WWW..WWW...x
x....WWWWWW......WWWWWW....x
x.....WWWW..A..B..WWWW.....x
xx.....WW..........WW.....xx
xxx......................xxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    bazaar_hellmonk_smoke_trees
TAGS:    no_rotate bazaar
ORIENT:  encompass
SHUFFLE: AB
SUBST:   A = {, B = >
KFEAT:   C = any shop
:        bazaar_setup(_G)
MAP
ttttttttttttttttttttttttttttttt
ttttttttttttllllllltttttttttttt
tttttttttttll.....llttttttttttt
tttttttttttl...C...lttttttttttt
tttttttttttll.....llttttttttttt
tttttttttttl...C...lttttttttttt
tttttttttttl.......lttttttttttt
tttttttttttll.A.B.llttttttttttt
ttttttttttttl.....ltttttttttttt
tttttttttttll..C..llttttttttttt
tttttttttttl.......lttttttttttt
tttttttttttl...C...lttttttttttt
tttttttttttll.....llttttttttttt
ttttttttttttllllllltttttttttttt
ttttttttttttttttttttttttttttttt
ENDMAP

NAME:    bazaar_hellmonk_stone
TAGS:    no_rotate bazaar
ORIENT:  encompass
SHUFFLE: AB
SUBST:   A = {, B = >
TILE:    i = dngn_transparent_stone
KFEAT:   C = any shop
:        bazaar_setup(_G)
MAP
ccccccccccccccccc
ccc....ici....ccc
ccC...i.c.i...Ccc
c....i..c..i....c
c...i...c...i...c
c..i....c....i..c
c.i....i.i....i.c
ci....i...i....ic
cccccc.A.B.cccccc
ci....i...i....ic
c.i....i.i....i.c
c..i....c....i..c
c...i...c...i...c
c....i..c..i....c
ccC...i.c.i...Ccc
ccc....ici....ccc
ccccccccccccccccc
ENDMAP


##########################################################################
# Rare maps (weights 0-10)
##########################################################################

# 4 shops
NAME:    bazaar_jpeg_honeycomb
WEIGHT:  10
TAGS:    no_rotate bazaar
ORIENT:  encompass
MONS:    plant
SHUFFLE: ABCDEF
SUBST:   A = >, B = >
SUBST:   . = .:1000 $
SUBST:   K = T:20 1 .:5
KFEAT:   C = any shop / distillery shop
KFEAT:   D = any shop / gadget shop
KFEAT:   E = any shop / weapon shop / armour shop
KFEAT:   F = any shop / book shop / scroll shop
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxK.....Kxxxxxxxxxxxxxx
xxxxxxxxxxxxx.........xxxxxxxxxxxxx
xxxxxxxxxxxx.....B.....xxxxxxxxxxxx
xxxxxx....Kxx.........xxK....xxxxxx
xxxx........xx.......xx........xxxx
xxxK....A....xxxx.xxxx....C....Kxxx
xxxx........xxK.....Kxx........xxxx
xxxxx.....Kxx.........xxK....xxxxxx
xxxxxxx..........{..........xxxxxxx
xxxxx.....Kxx.........xxK....xxxxxx
xxxx........xxK.....Kxx........xxxx
xxxK....F....xxxx.xxxx....D....Kxxx
xxxx........xx.......xx........xxxx
xxxxxx....Kxx.........xxK....xxxxxx
xxxxxxxxxxxx.....E.....xxxxxxxxxxxx
xxxxxxxxxxxxx.........xxxxxxxxxxxxx
xxxxxxxxxxxxxxK.....Kxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 4 shops
NAME:    bazaar_jpeg_ribbon
TAGS:    no_rotate no_pool_fixup bazaar
WEIGHT:  10
ORIENT:  encompass
SHUFFLE: zZ
SUBST:   z = ., Z = w
SHUFFLE: wl, ABCD
# special cases for blue/red floor
: local colour = random_bazaar_colour()
: lfloorcol(colour)
: if colour == "red" then
SUBST: l = w
: else
: if colour == "blue" then
SUBST: w = l
: end
: end
#
SHUFFLE: AX
SUBST:   X = {, A = >
SUBST:   B : T G
KFEAT:   C = any shop
KFEAT:   D = weapon shop / book shop / jewellery shop
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.xxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxx.x
x....xxxxxxxxxxxxxx...xxxxxxxxxxxxxx....x
x..A..xxxxxxxxxxx.......xxxxxxxxxxx..A..x
x......xxxxxxxxx....B....xxxxxxxxx......x
xx......xxxxxx.............xxxxxx......xx
xxx......xxxx.....wwZww.....xxxx......xxx
xxxx.............ww...ww.............xxxx
xxxxx......D.....w..X..z.....D......xxxxx
xxxx.............ww...ww.............xxxx
xxx......xxxx.....wwwww.....xxxx......xxx
xx......xxxxxx.............xxxxxx......xx
x......xxxxxxxxx....B....xxxxxxxxx......x
x..C..xxxxxxxxxxx.......xxxxxxxxxxx..C..x
x....xxxxxxxxxxxxxx...xxxxxxxxxxxxxx....x
x.xxxxxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxxxx.x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

# 4 random shops
NAME:   grunt_bazaar_doublecross
TAGS:   bazaar
DEPTH:  Bazaar
ORIENT: encompass
KFEAT:  A = any shop
SUBST:  T = G:20 T V U:1
:       bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxx
xT.xxxxxxxxxxx.Tx
x.A.xxxxxxxxx.A.x
xxx...xxxxx...xxx
xxxxx.......xxxxx
xxxxxxx.T.xxxxxxx
xxxxx.......xxxxx
xxx...xxxxx...xxx
x...xx+.{.+xx...x
xT.xxxx.T.xxxx.Tx
x...xx+.>.+xx...x
xxx...xxxxx...xxx
xxxxx.......xxxxx
xxxxxxx.T.xxxxxxx
xxxxx.......xxxxx
xxx...xxxxx...xxx
x.A.xxxxxxxxx.A.x
xT.xxxxxxxxxxx.Tx
xxxxxxxxxxxxxxxxx
ENDMAP

##########################################################################
# Very rare maps, including dangerous maps (weight 1)
##########################################################################

# 4.4 shops
NAME:    bazaar_jpeg_triangles
TAGS:    no_rotate bazaar
ORIENT:  encompass
WEIGHT:  1
SHUFFLE: ACD
SUBST:   A = {
KFEAT:   B = any shop / antique armour shop / jewellery shop
SUBST:   C = >
SUBST:   D = T
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxAxxxC............+...xxxxxxxxx
xxxxxxxx...xxx...........xxx...xxxxxxxx
xxxxxxx.....xxx....B....xxx.....xxxxxxx
xxxxxx.......xxx.......xxx.......xxxxxx
xxxxx....B....xxx.....xxx....B....xxxxx
xxxx...........xxx...xxx...........xxxx
xxxA............+...xxxD...........Dxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    bazaar_jpeg_hexagon
TAGS:    no_rotate bazaar
ORIENT:  encompass
WEIGHT:  1
SHUFFLE: AC, BD
KFEAT:   A = armour shop / weapon shop / gadget shop
KFEAT:   B = general shop
KFEAT:   C = scroll shop / book shop / distillery shop
SUBST:   D = >
SUBST:   = : +x
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxx.................===.xxxxxxxxx
xxxxxxxx.............A...xxx...xxxxxxxx
xxxxxxx.................xxx.....xxxxxxx
xxxxxx........{........xxx.......xxxxxx
xxxxx.................xxx....B....xxxxx
xxxx...A.............xxx...........xxxx
xxx.................xxx.............xxx
xxx+xxxxxxxxxxxxxxx+xxxxxxxxxxxxxxx+xxx
xxx+xxxxxxxxxxxxxxxx+xxxxxxxxxxxxxx+xxx
xxx.............===.................xxx
xxxx...........xxx.............C...xxxx
xxxxx....D....xxx.................xxxxx
xxxxxx.......xxx........>........xxxxxx
xxxxxxx.....xxx.................xxxxxxx
xxxxxxxx...xxx...C.............xxxxxxxx
xxxxxxxxx.xxx.................xxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    bazaar_jpeg_triangle_bulge
TAGS:    no_rotate bazaar
ORIENT:  encompass
WEIGHT:  1
SHUFFLE: AB
SUBST:   A = {, B = >
KFEAT:   C = any shop
:        bazaar_setup(_G)
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxCxxxxxxxxxxxxxx
xxxxxxxxxxxx.....xxxxxxxxxxxx
xxxxxxxxxx.........xxxxxxxxxx
xxxxxxxx....B...B....xxxxxxxx
xxxxxx.................xxxxxx
xxxx....C.....B.....C....xxxx
xxA.......................Axx
xxxxxxxxxxxxx...xxxxxxxxxxxxx
xxxxxxxxxxxxx...xxxxxxxxxxxxx
xxxxxxxxxxxx.....xxxxxxxxxxxx
xxxxxxxxxxx...C...xxxxxxxxxxx
xxxxxxxxxxxx.....xxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP