###############################################################################
# large_themed.des: Large regular vaults go here (these are defined by having an
#                   ORIENT line with argument, north, northwest etc. This file
#                   is for vaults which have some kind of theme, and tend to
#                   define a set of monsters. Use large_abstract.des for vaults
#                   which don't have a special theme.
#
# Note that all maps for the Vaults branch system (including Vaults:$) reside in
# vaults.des.
###############################################################################

default-depth: D:12-, !D:$, Depths, !Depths:$
# be careful to adapt these numbers for new vaults (using DEPTH)

###############################################################################
# Castle of kobolds and goblins
NAME:    minmay_goblin_kobold_castle
TAGS:    no_rotate
ORIENT:  north
DEPTH:   D:3-6
MONS:    goblin; hunting sling . stone / hobgoblin w:2; hunting sling . sling \
         bullet / nothing
MONS:    goblin; club / hobgoblin; club / gnoll w:3; club | spear / nothing
MONS:    kobold; stone / nothing
MONS:    kobold; club / big kobold w:1; club | short sword w:2 / nothing
SUBST:   y : y:48 w:1 W:1, Y : Y:48 W:1 .:1, y = wWW, Y = WW.
SUBST:   5 = 2 1:2 .:24
SHUFFLE: ABCD
KITEM:   D = gold
KMONS:   D = hobgoblin / kobold / gnoll / big kobold
SUBST:   ABC = 2
SHUFFLE: 12 / 34
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxx
xxxxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxx
xxxxwwwwcccccwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcccccwwwwxxxxxx
xxxwwwwwcAAAccccccccccccccccccccccccccccccccccccccccccccccccccccccDDDcwwwwwxxxxx
xxxwwwwwcAAAc1.....ccccc.....c5555555c1..1c5555555c.............1cDDDcwwwwwwxxxx
xxxwwwwwcAAAc.......ccc.2....+5555555c....c5555555+...........2..cDDDcwwwwwwxxxx
xxwwwwwwcc+cc........c1...2..ccccccccc....ccccccccc..............cc+ccwwwwwwwxxx
xxwwwwwwwc1..........c..2...........c.2..2.c..........cccccccc.....1cwwwwwwwwxxx
xxwwwwwwwc...........+..............+......+..........cccccccc.2....cwwwwwwwwwxx
xxwwwwwwwc1..........c..2...........c.2..2.c..........cccccccc.....1cwwwwwwwwwxx
xwwwwwwwcc+cc........c1...2..ccccccccc....ccccccccc..............cc+ccwwwwwwwwxx
xwwwwwwwcBBBc.......ccc.2....+5555555c....c5555555+...........2..cCCCcwwwwwwwwwx
xwwwwwwwcBBBc1.....ccccc.....c5555555c....c5555555c.............1cCCCcwwwwwwwwwx
xwwwwwwwcBBBccccccccccccccccccccccccccc++cccccccccccccccccccccccccCCCcwwwwwwwwwx
xwwwwwwwcccccwwwwwwwwwwwwwwwwwwwwwwwyY1..1Yywwwwwwwwwwwwwwwwwwwwwcccccwwwwwwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwyY....Yywwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyY.11.Yyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyx
xYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYY1..1YYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYx
x..............................................................................x
x..............................................................................x
ENDMAP

###############################################################################
#hidden cave vault, possibly with statue
#
NAME:    statuecave_lemuel
ORIENT:  northeast
DEPTH:   D:7-, Lair, Crypt:1-4
: if you.in_branch("Crypt") then
MONS:    orange crystal statue
: else
MONS:    ice statue / obsidian statue / orange crystal statue
: end
SHUFFLE: 1|/!*
SUBST:   *:*$, ?:x., !:., '=w.
ITEM:    ring of teleportation / ring of flight
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxwwwxxxxxxxxxxx
xxxxxxxxxxxxxxxxxwwwwwwwxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwxxxxxxx
xxxxxxxxxxxxxwwwwwwxxxwwwwwxxxxxx
xxxxxxxxxxxxwwwwwwxxxxxxwww.1xxxx
xxxxxxxxxxxwwwwxxxxxxxxxxwwW??xxx
xxxxxxxxxwwwwwxxxxxxxxxxxwwwW??xx
xxxxxxxxwwwwxxxxxxxxxxxxxxww|||xx
xxxxxxxxwwwxxxxxxxxxxxxxxxxw||dxx
xxxxxxxwwwxxxxxxxxxxxxxxxxxxxxxxx
xxxxxwwwwwxxxxxxxxxxxxxxxxxxxxxxx
xxxwwwwwwwwwxxxxxxxxxxxxxxxxxxxxx
wwwwwwwwwww''..................xx
.'wwwwwww''....................xx
..''wwww'......................xx
.....'w........................xx
ENDMAP


###############################################################################
# Black bear nests (Eino)
# Bears are wimpy when you meet them, so here's an early challenge.
#
NAME:   black_bear_nest_1
ORIENT: northwest
WEIGHT: 5
TAGS:   uniq_black_bear_nest
DEPTH:  D:4-8
NSUBST: ' = 1:1 / 2:R / *:.
KITEM:  R = royal jelly
MONS:   patrolling black bear
MAP
xxxxxxxxxxxxxxx
xx''.xxxxxxxxxx
x''.......xxxxx
x''...xx....xxx
xx''xxxxxxx..xx
xxxxxxxxxxxxx.x
xxxxxxxxxxxxxx@
ENDMAP

NAME:   black_bear_nest_2
ORIENT: southeast
WEIGHT: 5
TAGS:   uniq_black_bear_nest
DEPTH:  D:4-8
NSUBST: ' = 1:1 / 2:R / *:.
KITEM:  R = royal jelly
MONS:   patrolling black bear
MAP
xxxxxxxx@xx
xxxxxxxx.xx
xxxxxxxx.xx
xxxxxxx..xx
xxxxxx...xx
xxxxx...xxx
xxxx...xxxx
xxx...xx'xx
xxx..xx'.xx
xxx..x''.xx
@....xx'.xx
xxx...xx.xx
xxxx.....xx
xxxxx....xx
xxxxxxxxxxx
xxxxxxxxxxx
ENDMAP

###############################################################################
# Dragon's cave
#
NAME:    dragon1_lemuel
ORIENT:  northeast
DEPTH:   D:9-, Lair
MONS:    patrolling fire dragon, patrolling ice dragon
ITEM:    potion of flight / scroll of teleportation
SUBST:   ! : ?x
SHUFFLE: 1?/2W
SUBST:   2=2:20 1
SUBST:   ? : wl
SUBST:   * : $*|
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxx!!!xxxxxxxxx
xxxxxxxxxxxxxxxxx..????....dxxxx
xxxxxxxxxxxxxxx....???...1d**xxx
xxxxxxxxxxxxxx......???..$$**xxx
xxxxxxxxxxxx.........????$$!!xxx
xxxxxxxx.............??????!!!xx
xxx......xxxxx........????!!!!xx
xxx.xxxxxxxxxxx.......???!!!!!!x
xx..xxxxxxxxxxxxxx...????!!!!!!x
xx.xxxxxxxxxxxxxxxxxxxx!!!!!!!!x
xx.xxxxxxxxxxxxxxxxxxx!!!!!!!xxx
xx....xxxxxxxxxxxxxxxxxxx!!!xxxx
xxxxx.xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxx@xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Steading of the Giant Chief
#
NAME:    giant_chief_lemuel
DEPTH:   D:11-, Orc, Depths:2-
ORIENT:  north
SHUFFLE: ae / bB / gG / dD
SUBST:   ; = .'
: if you.branch() == "Depths" then
MONS:    patrolling iron giant, nothing, nothing
MONS:    ettin, stone giant w:40 / fire giant / frost giant
MONS:    ogre mage band / deep troll shaman band w:5 / \
         deep troll earth mage band w:5
MONS:    death yak / catoblepas w:2
NSUBST:  6 = 2:6 / *:.
KFEAT:   F = G
SUBST:   c = v
: else
MONS:    patrolling stone giant, patrolling frost giant, patrolling fire giant
MONS:    hill giant, cyclops
MONS:    w:50 ogre / w:5 ogre mage / two-headed ogre
MONS:    wolf / warg
SHUFFLE: 'F1/ww2
SHUFFLE: w2/l3
: end
SUBST:   C = ?5
SUBST:   a:. , e:. , b:x. , g:x. , d:x. , B:x. , G:x. , D:x. , ? = x.
SUBST:   0 = 4 7 8:20 .:20
SUBST:   | : |:30 Z
SUBST:   ! = +x
KFEAT:   Z = floor
KMONS:   Z = hill giant zombie / stone giant zombie / cyclops zombie /\
             titan zombie
KFEAT:   ( = .
KITEM:   ( = any weapon good_item
KMONS:   8 = plant
KFEAT:   8 = floor
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxccccccccccxxccccccccccccx
xxxx???xxxxxx?***??xxxxxxxxxxxxx.........7...c...444..cccc..........cx
xx$$$$$$?xxxx?.6..?xxxxxxxxxeeee..7..........+........+..+.........5cx
x?$$$$$$.?xx?.666.xxxxxxee0eexxx7....7.......c........cccc.........1cx
x.$$$$$$..?x...6..eeeeeeexxxxxxx............7ccccc+ccccccc.........5cx
x....<.....+?....xxxxxxxxx$$$..cccc+cccccccccccccc.c(((((+..........cx
x?........?xxxx.xxxxxxxx<+$$$..+.......+...........c(((((c.......***cx
xx........?xx...xxxxxxxxxxx!xxxc..4.4..ccccccccccccccccccc444....***cx
xxxx?...?xxxx.xxxxxxxxxxxxxCxxxc...4...cxxxCxxxxxxxxxxxxxxxxxxxxx+xxxx
xxxxxx!xxxxxx.xxxxxxxbbb;''''''c..4.4..c'''''';ddddddddddxxxxxxxx||xxx
xxx....xxxxx..xaaaaabbb0;''''''c.......c'''''';0ggggdddddd????xxx||xxx
x...xxxxx???0?aaaaabbbb..;'''''ccc+++ccc''''';...ggggggdddddd???xxxxxx
xx.xxx???......aaaabbbbb...;''F,''''''',F'';........ggggggdddd.???xxxx
x?!??.....0...aaaa..bbb........;''''''';.........ggggggg.ddd..0...?xxx
xx..........aaaaa....bbbbb.........4...ggggggggggggggg...dddd......??x
xx?...aaaaaaaaaa.......bbbbbbbbb..0..ggggggggggggggg...0....dd......?x
xxaaaaaaaaaaaa.....0.....bbbbbbbbbggggggggggggggg...........dd......?x
xxaaaaaaaa[....@..........bbbbb..@..gggggggg........@.......d.....@..x
ENDMAP

NAME:   minmay_smoke_and_mirrors
ORIENT: northwest
DEPTH:  D:10-, Depths
MONS:   wizard / boggart / nothing
MARKER: F = lua:fog_machine { cloud_type="purple smoke", \
            pow_min = 5, pow_max = 15, delay_min = 10,   \
            delay_max = 50, size_min = 1, size_max = 5,  \
            walk_dist = 0, spread_rate= 100 }
SUBST:  F = .
MAP
bbbbbbbbbbbbbbbbbbbbbbbbb
b....................Fbb.
b...bbbbb..1......1..bb..
b..bb***bb..........bb...
b.bb.....bb..1..1..bb....
b......1..bb......bb..1..
b..........bb||||bb......
b.1...bb...FbbbbbbF.....
b....bb....bb....bb.....
b...bb.1..bb......bb....
b..bb....bb........bb...
bbbb....bb....bb.1.Fbb..
b|.bb........bb....bb..
b...bb...1..bb....bb...
b....bb....bb....bb....
b1....bbbbbb....bb.....
bbb........bb.........
b|bb........bb.....1..
b.1bb........bb.......
b...bb........bb.....
b...Fbb.............
b...bb.............
b..bb....1......
b.bb........
b......
ENDMAP

NAME:   minmay_elf_outpost
TAGS:   no_rotate
ORIENT: northwest
DEPTH:  D:12-
MONS:   deep elf mage / nothing, deep elf knight / deep elf archer
MONS:   deep elf mage w:30 / nothing
MAP
xxxxxxxxxxxxxxxxxxxxxxxx
x..3..............|x1...
x.b..b..b..b3.b...|x....
x.....3.........3.|x..x.
x+xxxxxxx+xxxxxxxxxx....
x..................x....
x......1...........x..x.
x..xxxx....xxxxxx1.x1...
x.1x*.....1..2.*x..x....
x..x*.2.1......*x..x..x.
x..xxxxxx...1xxxx..x....
x..................x....
x1................1x..x.
xxxxxxx..xxxxxx..xxx....
x1................1x1...
x.......1....11....x..x.
x..xxxxxx....xxxx..x....
x..1.2.*x....x*....x....
x.1....*x....x*.2..x..x.
xxxxxxxxxx++xxxxxxxx....
x1......11..11.....1....
x.......................
x..x..x..x..x..x..x.....
x.......................
ENDMAP

NAME:   minmay_ice_pillars
TAGS:   no_rotate no_pool_fixup no_monster_gen no_tele_into
ORIENT: north
DEPTH:  D:11-, !D:$
WEIGHT: 5
MONS:   ice beast / nothing w:3, ice statue, ice dragon / nothing
SUBST:  y : y:48 w:1 W:1, Y : Y:48 W:1 .:1, y = wwW, Y = WW.
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxx
xxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwWWWWWWWWWWWWWWwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxx
xxwwWWWWWWWwwwwwwwwwwwwwwwwwWWWWWW............WWWWWWwwwwwwwwwwwwwwwwwWWWWWWWwwxx
xxwWW.....WWwwwwWWWWWwwwwwWWW.....1ccc++++ccc1.....WWWwwwwwWWWWWwwwwWW.....WWwxx
xwwW.1c.c1.WwwwWW...WWwwwWW..1cccccc........cccccc1..WWwwwWW...WWwwwW.1c.c1.Wwwx
xwwW.cc.cc.WwwwW.ccc.WwwwW..ccc..................ccc..WwwwW.ccc.WwwwW.cc.cc.Wwwx
xwwW.c*2*c.WWWWW.ccc.WWWWW..c>3.2..............2.3<c..WWWWW.ccc.WWWWW.c*2*c.Wwwx
xwwW.cc*cc.WwwwW.ccc.WwwwW..ccc..................ccc..WwwwW.ccc.WwwwW.cc*cc.Wwwx
xwwW.1ccc1.WwwwWW...WWwwwWW..1cccccc********cccccc1..WWwwwWW...WWwwwW.1ccc1.Wwwx
xwwWW.....WWwwwwWWWWWwwwwwWWW.....1cccccccccc1.....WWWwwwwwWWWWWwwwwWW.....WWwwx
xwwwWWWWWWWwwwwwwwwwwwwwwwwwWWWWWW............WWWWWWwwwwwwwwwwwwwwwwwWWWWWWWwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwWWWWWWWWWWWWWWwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwx
xyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyx
xYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYx
ENDMAP

NAME:   minmay_skeleton_keep
TAGS:   no_monster_gen
ORIENT: northwest
DEPTH:  Depths, Crypt
MONS:   flying skull / nothing w:30, skeleton w:20 / nothing
MONS:   skeletal warrior band / bone dragon w:1 / lich w:1 / curse skull w:1
SUBST:  G : GGccV.
MAP
cccccccccccccccccccccccccccccccc
c....2.....||c...+....111c|....c
c+c.2.3...cccc...c.....11cc..3.c
c.cc...2.2.ccc...c......1ccc...c
c..cc...2...cc...c...G...+2cc..c
c...cc.2...2.c...c......cc2ccc.c
c....cc...2..c...c1....cc2...+.c
c1.3..cc.2...+...c11..cc2.2.2c.c
c11....cc....c...c111cc2.2.22c|c
c111....cccccc.3.ccccccccccc+ccc
c1111....c...........c22.+11111c
cc+cc1.3.c.G2G2G2G2G.c2..cc1111c
c***c11..+...........+.2.2cc111c
c***c111.c.G2G2G2G2G.c2...2cc11c
c***c1111c...........c..22.2cc+c
cccccccccccccccccccccccccccccc@
ENDMAP

NAME:   minmay_spriggan_fort
TAGS:   no_rotate
ORIENT: northwest
DEPTH:  Depths
MONS:   plant, bush
MONS:   spriggan w:70 / spriggan druid / spriggan berserker / spriggan rider
MONS:   spriggan defender
SUBST:  1 : 12t p:1, p = 12t, c : xc, 3 = 3, 4 = 444.
MAP
ccccccccccccccccccccccccccccxx
c11111111111111111111111111c1.
c1.........3.............31c13
c1........................1c1.
c1..111........3.....111..1c1.
c13.1c1..3...........1c1..1c1.
c1..111..........3...111..1c1.
c1...........3............1c1.
c1.........3...3..........1c1.
c1.......111....111.......1c1.
c1.......1ccc++ccc1.......1c1.
c1.......1c1....1c1..3....1c1.
c1.......1c1....1c1.......1c1.
c1....3..1c14..41c1.......1c13
c1.......1c1****1c1.......1c1.
c1.......1c1****1c1.......1c1.
c1.......1c111111c1....3..1c1.
c1..3....1cccccccc1.......1c1.
c1.......1111111111.......1c1.
c1........................1c1.
c1........................1c1.
c1..111........3.....111..1c1.
c1..1c1..3...........1c1..1c1.
c1..111..............111..1c1.
c1........................1c1.
c13......................31c1.
c11111111111....11111111111c1.
ccccccccccccc++ccccccccccccc1.
x11111111111....11111111111113
x.3........3.3..3.............
ENDMAP

NAME:   minmay_haunted_quarter
ORIENT: northwest
DEPTH:  Depths, !Depths:$
KMONS:  w = wraith / freezing wraith / shadow wraith / phantasmal warrior / \
            flayed ghost / nothing w:950
KFEAT:  w = w
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xcccccccccccccccccxxxxxxxxxx
xc|||...wcxw..>..wxw..>..wx.
xc|>|c.wwcxww.x.wwxww.x.wwx.
xc|||cwwwcwwwxxwwwxwwwxwwwx.
xcccccwwwcwwwxwwwxxwwwxwwwx.
xccwwwwwwcwwwxwwwxwwwxxwwwx.
xcwwwwwwwcwwwxwwwxwwwxwwwxx.
xc.wwwwwccwwwxwwwxwwwxwwwx.
xc.ccccccwwwxxwwwxwwwxwwwx.
xc>cwwwwwwwwxwwwxxwwwxwwwx.
xc..wwwwwwwwxwwwxwwwxxwwwx.
xc.wwwwwwxxxxwwwxwwwxwwwxx.
xcwwxxxxxxwwwwwwxwwwxwwwx.
xxxxxwwwwwwwwwwxxwwwxwwwx.
xxwwwwwwwwwwwwxxwwwxxwwwx.
xx.wwwwwwwxxxxxwwwwxwwwxx.
xx..wxxxxxxwwwwwwwwxwwwx.
xx>xxxwwwwwwwwwwwwxxwwwx.
xx..wwwwwwwwwwwxxxxwwwxx.
xx.wwwwwwwwxxxxxwwwwwwx.
xxwwwwxxxxxxwwwwwwwwwxx.
xxxxxxxwwwwwwwwwwwwxxx.
xxwwwwwwwwwwwwwwxxxx..
xx.wwwwwwwwwxxxxx...
xx..wwwxxxxxx....
xx.xxxxx.....
x.......
ENDMAP

#########################################################################
# Orc temple
#
NAME:   orc_temple
ORIENT: southwest
TAGS:   no_rotate
DEPTH:  Depths, !Depths:$, Orc
MONS:   orc warlord, patrolling orc priest, patrolling orc high priest
MONS:   orc warrior, orc wizard, orc knight, orc sorcerer
KFEAT:  C = altar_beogh
MAP
xxxxxxxxxxxxxxx@xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxx.xxxxxxxxxxxxxxxx
xxxxxxxxxxxxxx...xxxxxxxxxxxxxxx
xxxxxxxxxxxxxx4.4xxxxxxxxxxxxxxx
x**..........x414x..........**xx
x**..........x4.4x..........**xx
x............+...+....4.......xx
x....4..4....x...x............xx
x............x...x.......4....xx
x............xx.xx............xx
x...4......xxxx+xxxx......6...xx
x........xxx.......xxx........xx
xxx...xxxx..2.....2..xxxx...xxxx
xxxx+xxxx.............xxxx+xxxxx
xxx...xxx.............xxx...xxxx
x......x...............x......xx
x..4...x...2...C...2...x...5..xx
x......x...............x......xx
x...4..xx.............xx..5...xx
x$......x....2...2....x......$xx
x$6..5..xx.....3.....xx.5...7$xx
x$$$.....xxx.......xxx.....$$$xx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# Fort Yaktaur (Matthew Ludivico)
#
NAME:   fort_yaktaur
ORIENT: southeast
DEPTH:  Depths, !Depths:$
MONS:   yaktaur, death yak, minotaur, random
MONS:   yak, gnoll, random
MAP
.........@....wwwwwwwwwwwwwwwwwxx
.ccccc.......ww....wwww....wwwwxx
.c$c%c......ww.ccccccccc.......xx
.c+c+c......ww.c.%$....ccccccccxx
.c...+......ww.c*.115..c$$+|*|cxx
.c1..c.....ww..c...55+ccc+cxx+cxx
.ccccc.....ww..ccccccc....c|+*cxx
............ww.......c5...cxx+cxx
....6.ccccc.ww.w...2.+51..c|1.cxx
....63+...c..wwww..21+51..c2.2cxx
....6.ccccc..wwwwww..c5...cc+ccxx
............wwwwwww..c........cxx
............wwwwwww..ccccccccccxx
...........ww1w..www...........xx
.......566.www.....www.........xx
.........1ww....ccccc..........xx
.....566.w......+...c..........xx
.........www....ccccc..........xx
...........ww............wwwwwwxx
.......3....wwwww......www.....xx
......666.......ww...www.......xx
.....cc+cc.......wwwww.........xx
.....c...c.....................xx
.....ccccc.....................xx
...............................xx
...............................xx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Rubicon
#
NAME:    erik_rubicon_a
DEPTH:   D:10-, !D:$
ORIENT:  south
TAGS:    no_pool_fixup no_rotate uniq_rubicon
WEIGHT:  6
ITEM:    ring of flight / potion of flight / nothing
ITEM:    potion of flight / nothing, nothing, nothing, nothing
SHUFFLE: {[(, )gh
SUBST:   ?:?.w, ?=w., w:lw, ':'.c, '=cx.
MAP
xxxxxxxxxcccccccc????w.@??ww.@www???w...@???wwww.@ww??@..?ccccxxxxxxxxxxx
xxxxxccccwwwwwwwww..wwwww.wwwwwwww.wwwwwww.wwwwwwwwwwww??wwwwcccccccxxxxx
xxxxxcwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcxxxxx
xxxxxcwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcxxxxx
xxxxxccwwwwwwwwwwwwwwwwwwwcccwwwwwwwwwcwwwwwwwwwwwwwwwwwwwwwwcccwcccxxxxx
xxxxxcwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcxxxxx
xxxxxcwwwwwwwwwwwwwwwwwwwwwccwwwwwwwwwcwcwwwwwwwwwwwwwwwwwwwwwwcccccxxxxx
xxxxxcwwwwwwwwwwwwxwwwwwwwwwwwwwwwwwwwcwwwww.wwwwwwwwwwwwwwwwwwwwwwcxxxxx
xxxxxccwcwwwwwwwwwwwwwwwxwwwwww???wwwwwwwww???wwwwwwwww.wwwwwwwcccccxxxxx
xxxxxcccccc????w??.w????xw??w??...?wwww???w????w?w???w???w??cccccxxxxxxxx
xxxxxxxxxcxx...''....?..xx........??w??...c''....??.....???..cxccxxxxxxxx
xxxxxxxxxcx..(cc[.''..'xx...x.''.x..?xx.xcc..cxcx...xxx.'...chxxcxxxxxxxx
xxxxxxxxxcx..cc{c..xx.....xx...cc..xx....xcc....''xxx..x.xxc...cxxxxxxxxx
xxxxxxxxxccx....e.xx....x..x.....c....cx..x...x......c......cxcxxxxxxxxxx
xxxxxxxxxxcxexxx.x.x.xx.x.xx.xcc.xx.xccxx....xx.x.xxxccxx.xcccxxxxxxxxxxx
xxxxxxxxxxccx.gxxx..x..xdx..x.xxxx.x...xx.xx...xx.xxxx)..xxcxxxxxxxxxxxxx
xxxxxxxxxxxcxxxxxxxx...xxxxxxxxxccccccc..xxxxccccccccxxxccccxxxxxxxxxxxxx
xxxxxxxxxxxcccccccccccccccccccccccccccccccccccxxxxxcccccccxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:    erik_rubicon_b
DEPTH:   D:10-, !D:$
ORIENT:  south
TAGS:    no_rotate uniq_rubicon
WEIGHT:  4
ITEM:    ring of flight / potion of flight / nothing
ITEM:    potion of flight, nothing, nothing, nothing, nothing
SHUFFLE: {[(, fgW, )hi
SUBST:   ?:?.wW, ?=wW., ':'.c, '=c.
MAP
xxxxxcccccccc?gw?f.@wgf?.@w??g?fw...@gwfw???.@wwww@..?ccccxxxxxxx
xccccwwwwwww??..f????.ww???www.???ww??.wwwwwwwwwwwffgg?wwcccccccx
xcwwwwwwwwwwww??wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww??wwwwwwwwwcx
xcwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwcx
xccwwwwwwwwwwwwwwwgwwwcccwwwfwwwwwcwwwwgwwwwwwwwwwwwwwwwwcccwcccx
xcwwwwwwwwwwwwwwgwwwwwwwwwwwwfwwwwwwwwwwwwwfwwwwwwwwwwwwwwwwwwwcx
xcwwwwwwwwwwwwwwwwwwwwwccwwwwwwwwwcwcwwww?wwwwwwwwwwwwwwwwwcccccx
xcwwwwwwwwwwwwxwwwwwwwwwwwwwwwwwwwcwwwww.wwwwwwwwwwwwwwwwwwwwwwcx
xccwcwwwwwg?wwwfwwwwxwwwwwwwwwwwwwwwwwwwfgwwwgwwwww.wwwwwwwcccccx
xccccccwffwg??fw?ww?gx??ww?..??wwwww??w?fwgw?fwgw??fg??gcccccxxxx
xxxxxcxx...'''...?..xx.....''.?ww??...c.'....??...'.?gg.xcxccxxxx
xxxxxcx..(cc[.'.'.xx..'.x.'..x..?...xcc..cxcx.x..x..''..cxhxcxxxx
xxxxxxcc..c{c..x.x...c.xxx.x.xx.'.'xx..cc..c.xx''.cc..xxx...xxxxx
xxxxxccx....e.xx...x...x.....c...c....cc....x...x....x....cxxxxxx
xxxxxxcxexxxxx.x.xxxx.x..xcc.xx..cc.x....xx.x.x.xccxx.xcccxxxxxxx
xxxxxxccx..xi.x.x.xxdxxxx...x..xx..xxxxx...x).xx..xc.xxcxxxxxxxxx
xxxxxxxcxxxxxx...x....xxxxxxcccccccxxxxxxccccccccx..ccccxxxxxxxxx
xxxxxxxcccccccccccccccccccccccccccccccccccxxxxxcccccccxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# The Lair of the Golden Dragon (by Mu.)
#
NAME:   gold_lair_mu
ORIENT: southeast
TAGS:   no_monster_gen
DEPTH:  Depths, !Depths:$
MONS:   golden dragon, any draconian, fungus, plant, oklob plant
KMONS:  6 = rime drake / swamp drake / lindwurm
KMONS:  D = rime drake w:5 / swamp drake w:5 / death drake / lindwurm w:5
KITEM:  D = $
SUBST:  $ = $|
SUBST:  f : A B
SUBST:  A = 3:30 .:90 5:1
SUBST:  B = 4:30 .:120 5:1
SUBST:  m = W:20 w:10
KFEAT:  o = granite_statue
MAP
        ...........ffffmmmmmmmxxx
       ..........ffmmmwwwwwwwmxxx
      .........ffmmwwwwwwwwwxxxxx
     ..6....ffffmmwwwwwxxxxxxxxxx
    ..9...ffffmmwwwwwxxxxxxxxxxxx
   ..9..ffffmmwwwwwwxxxxxxxxxxxxx
  ..6..fffmmwwwwwwwxxxxx.2x.2xxxx
 .....fffmwwwwwwwwxxx........xxxx
......ffmwwwwwwwwxxx.....xx...xxx
.....fffmwwwwwwxxxxx...xxxxx..xxx
.....ffmwwwwwwxxxxxx..xxxxxxx..xx
....fffmwwwwwxxxx.....xxxoxxxx.xx
....ffmwwwwwxxxx..6..xx....xx..xx
...fffmwwwwxxxxx..xxxx...6....cxx
...ffmwwwwwxxxx..xxxxx..xxxx2xxxx
..fffmwwwwxxxx...xxxx...ccccccccx
.fffmwwwwwxxx....xxx..ccccccccccx
..ffmwwwwxxxx..x..xx.cccc.6..cccx
.fffmwwwxxxxx..xx..xc.c.......ocx
.fffmwwwxxxx..xxx..ccc....lllllcx
..ffmwwxxxx..xxxx.xcc....llllllcx
.fffwwxxxx..xxxxx.ccc...llll$$Dcx
.fffwwxxx..x..2xx.cc...llll$$$$cx
..ffwwxxx....xxx..cc6.llll$$$$$cx
.ffffwxxxx..xxx..xcc..lll$$$$$$cx
..8ffwwxxx...xx2xxcc..ll$$$$$$$cx
.6.fffwwxxx..xxxxxccc.ll$$$$$1ccx
....fffmwwwxxxxxxxcccollD$$$$cccx
.....fffmmwwwwwwwwccccccccccccccx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# The Golem Gauntlet (by Mu.)
#
# Each room is progressively more challenging; both the golems, and their
# creators.
#
# (With the drop-off of golems, this vault has been revamped into general
#  "constructs". hangedman sends his regards.)
NAME:   constructs_gauntlet_mu_hangedman
DEPTH:  Depths, !Depths:$
TAGS:   no_monster_gen no_item_gen no_pool_fixup
ORIENT: east
MARKER: m = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 1, \
                size_buildup_time = 25, cloud_type = "flame" \
            }
MARKER: z = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 40, \
                size = 2, size_buildup_amnt = 1, \
                size_buildup_time = 25, cloud_type = "freezing vapour" \
            }
MONS:   gargoyle, earth elemental, air elemental
MONS:   dancing weapon ; long sword good_item, crystal guardian, war gargoyle
KFEAT:  o = granite_statue
KFEAT:  m = .
KFEAT:  z = .
KMONS:  A = wizard / Erica, wizard
KMONS:  B = orc sorcerer / Nergalle, orc sorcerer
KMONS:  C = ogre mage / Erolcha, ogre mage
KMONS:  D = deep elf sorcerer
KMONS:  E = lich / Boris, lich
KMONS:  F = ancient lich
MAP
 cccccccccccccccccccccccccccccc
 cccccccccccccccccccccccccccccc
 cc......+.....cc......+....$cc
 ccmcccccc.....cczcccccc...6$cc
 cc.....cc*.E5.cc.....cc..F.$cc
 ccccccmcc|.5..cccccczcc.6..$cc
 cc.....cc*$$..+......cc|||||cc
 cc.ccccccccccccccccccccccccccc
 cc+ccccccccccccccccccccccccccc
 cc.....cllllllcc%$$..cwwwwwwcc
 cc...4.clcccclcc$..3.cwccccwcc
 cc..D..+.cllllcc$3C..+.cwwwwcc
 cc%4...ccclccccc..3..cccwccccc
 cc$$$%%ccclllll+.....cccwwwwcc
 cccccccccccccccccccccccccccwcc
 ccccccccccccccccccccccccccc+cc
.cc......+...$$ccWWWWWW+.....cc
.cc.cccccc..1.$ccWcccccc...2.cc
.cc.....cc..A..ccWWWWWWc..B..cc
occcccc.cc..1..cccccccWc.2.1$cc
@+......cc.....+WWWWWWWc...$%cc
occcccccccccccccccccccccccccccc
 cccccccccccccccccccccccccccccc
ENDMAP

##############################################################################
# Gold mine. Can come up in the dungeon and in Orc.
#
NAME:   minmay_goldmine
ORIENT: southeast
DEPTH:  D:8-, Orc
MONS:   orc / orc wizard w:3 / orc priest w:2 / orc warrior w:2
SUBST:  X = xxxZ
KPROP:  Z = no_tele_into
SUBST:  Z = .$
MAP
xxxxxxxxxxxxxxxxxxxxxxxxx
xXXXXXXXXXXXXXXXXXXXXXXXx
.XXX1XXXXXXXXXXXXXXXXXXXx
@..XX..XXXXXXXXXXXXXXXXXx
x.X..X1XXXXXX.XXXXXXXXXXx
xXXXX.XXXXXXX.X1XXXXXXXXx
xXX1.X.XXXXXXX.XXXX.XXXXx
xXXXXX..XXXXX1XXXXXX1XXXx
xXXXX.XX1XXX...XXXX..XXXx
xXXXX.XXX..X.XX.XX.XX1XXx
xXXX1XXXX.X1XXXX.1XXX.XXx
xXXXXXXXXXXXXXXXXXXX.XXXx
xXXXXXXXXXXXXXXXXXXXXXXXx
xXXXXXXXXXXXXXXXXXXXXXXXx
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_crystal_thing
TAGS:   no_monster_gen no_item_gen no_pool_fixup
ORIENT: west
DEPTH:  Depths, !Depths:$
MONS:   crystal guardian, orange crystal statue
ITEM:   any magical staff / any rod w:2 / any book / any jewellery w:15 / \
        any scroll w:20 / potion of magic / potion of brilliance / \
        fan of gales w:1 / lamp of fire w:1 / \
        stone of tremors w:1 / phial of floods w:1 / gold w:30
KFEAT:  _ = altar_sif_muna
MAP
            bbbbb
            bdddb
            bd1db
            bdddb
     bbbbbbbbb.bbbbbbbbb
     bdddb.........bdddb
     bd1d....._.....d1db
     bdddb.........bdddb
     bbbbbWW.....WWbbbbb
         bwW.....Wwb
         bwWW1.1WWwb
         bwwWW.WWwwb
         bbwwW.Wwwbb
          bbwW.Wwbb
           bwW.Wwb
           bwW.Wwb
           bwW.Wwb
          bbwW.Wwbb
         bbwwW.Wwwbb
        bbwwWW.WWwwbb
       bbwwWW...WWwwbb
      bbwwWW..W..WWwwbb
     bbwwWW..WWW..WWwwbb
    bbwwWW..WWwWW..WWwwbb
   bbwwWW..WWwwwWW..WWwwbb
  bbwwWW..WWwwwwwWW..WWwwbb
 bbwwWW..WWwwbWbwwWW..WWwwbb
bbwwWW1.WWwwwW2WwwwWW.1WWwwbb
 bbwwWW..WWwwbWbwwWW..WWwwbb
  bbwwWW..WWwwwwwWW..WWwwbb
   bbwwWW..WWwwwWW..WWwwbb
    bbwwWW..WWwWW..WWwwbb
     bbwwWW..WWW..WWwwbb
      bbwwWW..W..WWwwbb
       bbwwWW...WWwwbb
        bbwwWW.WWwwbb
         bbwwW.Wwwbb
          bbwW.Wwbb
           bwW.Wwb
           bwW.Wwb
           bwW.Wwb
        bbbbwW.Wwbbbb
        bwwwwW.Wwwwwb
        bwWWWW.WWWWwb
        bwWb.....bWwb
bbbbbbbbbwW.......Wwbbbbbbbbb
bwwwwwwwbwW.......Wwbwwwwwwwb
bwWWWWWwbwW.......WwbwWWWWWwb
bwWb.bWwbwW.......WwbwWb.bWwb
bwW.1.WwwwW.......WwwwW.1.Wwb
bwW...WWWWWb.....bWWWWW...Wwb
bwW.......................Wwb
bwW...WWWWWb.....bWWWWW...Wwb
bwW.1.WwwwW.......WwwwW.1.Wwb
bwWb.bWwbwW.......WwbwWb.bWwb
bwWWWWWwbwW.......WwbwWWWWWwb
bwwwwwwwbwW.......Wwbwwwwwwwb
bbbbbbbbbWW.......WWbbbbbbbbb
        xxxx.....xxxxxxxxxxxx
           xx+++xxxxxxxxxxxxx
            x................
            x...............@
            x................
            xxxxxxxxxxxxxxxxx
ENDMAP

NAME:   minmay_twisted_water
ORIENT: north
DEPTH:  D:9-
SUBST:  W = wW
MONS:   ice statue, patrolling ice dragon, patrolling frost giant, ice beast
KMONS:  w = w:490 nothing / ice beast
KFEAT:  w = w
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxWxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxwwxxxx*xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxwwwwxx.xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxwwwxxxWWxxxWwxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxwwxwwwwxx*xxwxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxwwwwwxxxxxwwxxxxxxxxxxxwwW.xxxxx
xxxxxxxxxxxxx..xxwwwwwxxxwwwxxxxxxxxwwwwwWWWwxxx
xxxxxxxxxxx.....Wwwxwwwwwwwwxxxxxxwwwwwwwwwwwwxx
xxxxxxxxxxx.....xwwxwwwwwxxxxxxxxwwwwwwwwwwwwwxx
xxxxxxxxxxxx.3.xxxwxxwwxxxxxxxxxxwwwwwwwwwwwwwwx
xxxxxxxxxxxxWWWxxxwxxxxxxxxxxxxxwwwwwwxxxwwwwwwx
xxxxxxxxxxxwwwwxxxwwxxxxxxxxxxxxwwwxxxxxxxxwwwwx
xxxxxxxxxxwwwwxxxxwwwxxxxxxxxxxwwwxxxxxxxxxwwwwx
xxxxxxxxxxxwwwwwwwwwwxxxxxxxxwwwwxxxx**|xxwwwwxx
xxxxxxxxxxxxxwwwwwwwwwwxxxwwwwwwxxxxWW2*xxwwwwxx
xxxxxxxxxxxxxwwwwwwwwwwwwwwwwwwxxxxxwwWWxwwwwwxx
xxxxxxxxxxxxWwwwwwwwwwwwwwwwwwwxxxxxwwwwwwwwwxxx
xxxxxxxxxxx.Wwwwwwwwwwwwwwwwwwxxxxxxxwwwwwwwxxxx
xxxxxxxxxxxWWwwwwwwwwwwwwwwwwwxxxxxxxxxwwwxxxxxx
xxxxxxxxxxxxwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwwwwwwwwwwxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxwwwwwWWWwwwwwwxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxwwwwwwW1Wwwwwwwxxxxxxxxxxxxxxxxxxx
xx*Wxxx*xxwwwwwwwwwwWWWwwwwwwwxxxxxxxxxxxxxxxxxx
xxxWxxWWxxwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxxxx
xxxwxxwxxwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxxx
xxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxxxxxxxxx
xxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW.xxxxxxxxxxxxxx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwWWWwwxxxxxxxxxxx
xwwxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxxxxx
xWxxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxxxx
x*xxxWwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxxx
xxxx.Wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxx
xxxxxWwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxx
xxxxxwwwwwwwwwwwwww...........wwwwwwwwwwwwwwwwwx
xxxwwwwwww..........................wwwwwwwwwwwx
xwwwww..................................wwwww...
................................................
ENDMAP

NAME:   due_jory_tower
ORIENT: southwest
TAGS:   no_rotate no_monster_gen no_item_gen
DEPTH:  Depths, !Depths:$
MONS:   flayed ghost w:5 / phantasmal warrior w:5 / phantom / shadow / shadow wraith
MONS:   ghost moth, Jory, vampire knight
MONS:   vampire mosquito
NSUBST: A = 3:v / *:+, B = 3:v / *:+
NSUBST: C = 2:v / *:+, D = 2:v / *:+
# Colouring
COLOUR: G = cyan
COLOUR: 5M% = white
# Making things other things.
SUBST:  I = G
# Markers:
MARKER: V = lua:fog_machine { \
                pow_max = 10, delay_min = 5, delay_max = 30, \
                size = 30, size_buildup_amnt = 1, \
                size_buildup_time = 10, cloud_type = "purple smoke" \
            }
# Final subst
SUBST:  MV = .
:       set_feature_name("granite_statue", "iron statue")
:       dgn.delayed_decay(_G, '%', 'human corpse / human skeleton')
veto {{
    if you.uniques("Jory") then
      return true
    else
      return false
    end
}}
MAP
  @@@@@@ @@@@@@@@@@@@@@@@@@@ @@@@@@
xx......v...................v.......xxxxxxxxxxxxxxxxx
xx......v..G.............G..v.......xxxxxxxxxxxxxxxxx
xx......v.........1.........v.......xxxxxxxxxxxxxxxxx
xxxxxxxxvvvvvv.........vvvvvvxxxxxxxxxxxxxxxxxxxxxxxx
xxxxvvvvvvvvvvvvv+++vvvvvvvvvvvvvxxxxxxxxxxxxxxxxxxxx
xxxvv.....vvvvvv.....vvvvvv.....vvxxxxxxxxxxxxxxxxxxx
xxxv.......+vvv...Y...vvv+.......vxxxxxxxxxxxxxxxxxxx
xxxvvvV1.vvv.vvv.....vvv.vvv.1Vvvvxxxxxxxxxxxxxxxxxxx
xxxv.......vv.vv+++++vv.vv.......vxxxxxxxxxxxxxxxxxxx
xxxvv.....vvv.vv.1.1.vv.vvv.....vvxxxxxxxxxxxxxxxxxxx
xxvvvvv+vvvvv.v...G...v.vvvvv+vvvvvxxxxxxxxxxxxxxxxxx
xxv.B1+.+1vvv.vv.1.1.vv.vvv1+.+1A.vxxxxxxxxxxxxxxxxxx
xxv.vvv.vvvvvv.vv+++vv.vvvvvv.vvv.vxxxxxxxxxxxxxxxxxx
xxv.B1+.+1Cvvvv+v...v+vvvvD1+.+1A.vxxxxxxxxxxxxxxxxxx
xxv.vvv.vvv..Vvv.....vvV..vvv.vvv.vxxxxxxxxxxxxxxxxxx
xxv.B1+.+1C.4.vvvvvvvvv.4.D1+.+1A.vxxxxxxxxxxxxxxxxxx
xxv.vvv.vvv..|vv*|||*vv|..vvv.vvv.vxxxxxxxxxxxxxxxxxx
xxv.B1+.+1Cvvvvvv.*.vvvvvvD1+.+1A.vxxxxxxxxxxxxxxxxxx
xxv.vvvvvvvvv.V.vv+vv.V.vvvvvvvvv.vxxxxxxxxxxxxxxxxxx
xxv.........+.1.v4.4v.1.+.........vxxxxxxxxxxxxxxxxxx
xxvvvvvvvvvvv...v.3.v...vvvvvvvvvvvxxxxxxxxxxxxxxxxxx
xxxvvvvvvvvvvv+vv...vv+vvvvvvvvvvvvxxxxxxxxxxxxxxxxxx
xxvvMMMMMMMvvv.vv.Y.vv.v...v...vvvvxxxxxxxxxxxxxxxxxx
xxv555%%%MMMvv.vv...vv.+.2.".2.+..vxxxxxxxxxxxxxxxxxx
xxv555%I%MMM+..vv+++vvvv...v...vv.vxxxxxxxxxxxxxxxxxx
xxv555%%%MMMvvvv.....vvvvvvvvvvvv.vxxxxxxxxxxxxxxxxxx
xxvvMMMMMMMv+.+..111..+.........+.vxxxxxxxxxxxxxxxxxx
xxvvvvvvvvvvvvvv..V..vvvvvvvvvvvvvvxxxxxxxxxxxxxxxxxx
xxxvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvvxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Wizard Prison
#
NAME:   evilmike_wizard_prison
TAGS:   no_monster_gen
DEPTH:  Depths, !Depths:$, Crypt:1-4
ORIENT: southeast
NSUBST: 3 = 3:. / *:3
SUBST:  ^ : ^mmm
KFEAT:  ^ = permanent teleport trap
KFEAT:  m = iron_grate
KFEAT:  y = rock_wall
KMASK:  y = no_wall_fixup
KPROP:  -d|*$14567^ = no_tele_into
FTILE:  -mcyd|*$14567^! = FLOOR_PEBBLE_DARKGRAY
FTILE:  .@+23 = FLOOR_HALL
TILE:   c = DNGN_STONE_WALL_DARKGRAY
# This is to mark the wall clearly diggable.
TILE:   y = WALL_NORMAL
MONS:   patrolling silent spectre
MONS:   patrolling curse skull, soul eater
# Inmates are monsters capable of blinking, summoning or airstriking.
MONS:   ogre mage / deep elf mage / wizard / necromancer / \
        merfolk aquamancer / spriggan air mage / orc sorcerer / \
        deep elf sorcerer / kobold demonologist / deep elf mage
MONS:   draconian shifter / draconian annihilator / lich  / \
        deep elf annihilator / deep elf demonologist
NSUBST: 4 = 6:5 / *:4
MONS:   ancient lich
MONS:   human att:neutral
NSUBST: 4 = 1:7 / *:4
ITEM:   acquire any
SUBST:  - = .
MAP
xxxxxxxxxxxc.@.cxxxxxxxxxxx
xxxxxxxxxxxc...cxxxxxxxxxxx
xxxxxxxxxxxc.c.cxxxxxxxxxxx
xxxxxxxxxxxc...cxxxxxxxxxxx
xxxxcccccccc.c.ccccccccxxxx
xxxxc-4cc--c...c4-cc--cxxxx
xxxxc--cc4-c3.3c--cc-4cxxxx
xxxccmmccmmcc+ccmmccmmccxxx
xxxc3..................cxxx
xxxc3................3.cxxx
xxxcmmcmmc+cmmcmmcmmc..cccx
xxxc--c-4c-c4-c4-c-4c..m4cx
xxxc-4c--c-cccccccccc..m-cx
xxxccccccc---^1y|c--m.3cccx
xxxccccccc-ccccc|c-4m..m4cx
xxxc4-c--c-c-4c||cccc..m-cx
xxxc--c-4c-c--cddc-4c..cccx
xxxcmmcmmc+cmmccccmmc..cxxx
xxxc3..................cxxx
xxxc3.................3cxxx
xxxccmmcmmcmmc..cmmcmmccxxx
xxxxc4-c-4c--c..c--c-4cxxxx
xxxxc--c--c4-c..c4-c--cxxxx
xccccccccccycc.2cccccccxxxx
xc|$*$*$cccy-ccccxxxxxxxxxx
xc$|$*$*-6-y-cxxxxxxxxxxxxx
xcccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Makhleb temple
#
NAME:   evilmike_makhleb_hellhole
ORIENT: southeast
DEPTH:  Depths, !Depths:$
TAGS:   no_monster_gen
COLOUR: v = lightred
COLOUR: = = lightred
TILE:   v = DNGN_METAL_WALL_BROWN
FTILE:  .Yv+*|m1234567d$' = FLOOR_INFERNAL_BLANK
FTILE:  = = FLOOR_INFERNAL_BLANK
KFEAT:  7 = altar_makhleb
KFEAT:  m = iron_grate
NSUBST: * = 2:| / *:*
SUBST:  Z = Y, Y = Y:4 V:1
SUBST:  v = v:6 b:1, . = .:7 ':1
KPROP:  'b6 = bloody
SUBST:  b = v, B = .
MONS:   hell knight
MONS:   kobold demonologist, deep elf demonologist
# these demons are the same ones you get from greater/lesser servant
MONS:   blizzard demon / green death / patrolling executioner / balrug / cacodemon
MONS:   orange demon / ynoxinul / w:4 neqoxec / hellwing / smoke demon
# depth controls the number of greater servants... amount ranges from 1 to 3
: if you.absdepth() < 21 then
NSUBST: 4 = 1:4 / 2:5
NSUBST: 3 = 1:3 / 1:9
: elseif you.absdepth() < 24 then
NSUBST: 4 = 2:4 / 1:5
: end
MONS:   human att:neutral
KMONS:  7 = patrolling hell knight
ITEM:   acquire any
SUBST:  '" = ., b = v
MAP
          xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
          xxxxvvvvvvvvvvvvvvvvvvvvxxvvvvvvv
          xxxxvvvvvvvvv.1vv.2vv.1vvvv...vvv
          xxxxvllvvvvvv..vv..vv..vvv..Y.3vv
          xxxxvlY......vv$$vv$$vv....vvv**v
          xxxxvv.......vv$$vv$$vv....vvv**v
          xxxxvv.2.vvvv..vv..vv..vvv..Y.3vv
          xxxxvv...vvvv.2vv.1vv.2vvvv...vvv
          xxxxvv...vvvvvvvvvvvvvvvvvvllvvvv
          xxxxvv...vvvvvvvvvvvvvvvvvllvvvvv
   xxxxxxxxxxxvvv+vvvvvvvlvllvllvllllvvvvvv
   xvvvvvvvvvvvv...vvvvvvllllllllllvvvvvvvv
  ""vlvlvlvlvvv.....vvvv..............vvvvv
"Z"Zv....1..vv...Y..1vvv..v.5v..v..v.4*|vvv
@"""v......1v...lll...vY..Y..Y..Y.1Y.vv||vv
@"""+.......+..YlvlY.5+.............7lvv|dv
@"""v......1v...lll...vY..Y..Y..Y.1Y.vv||vv
"Z"Zv....1..vv...Y..1vvv..v.5v..v..v.4*|vvv
  ""vlvlvlvlvvv.....vvvv..............vvvvv
   xvvvvvvvvvvvv...vvvvvvllllllllllvvvvvvvv
   xxxxxxxxvvvvvv+vvvvvvvlvllvllvllllvvvvvv
          xvvvvv...vvvvvvvvvvvvvvvvvllxxxxx
          xv.......m6''vxxxxxxxxxxxxxllxxxx
          xv.5..5..m'l6="xxxx""0""xxxxllxxx
          xv.vvv.5.m'6'vx"xxxx"xxx"xxxxll"x
          xv.vvvv..vv=vvx"xx""xxxx"xxxxl>"x
          xv$vvvv..m'6'vxx""xxx"x"xxxxx"""x
          xv**vvv4.m6l'vxx"xxxxx"xxxxx"""xx
          xv**$....m'''vxx"0xxxxx""0""xxx"x
          xvvvvvvvvvvvvvxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Sif Muna library
#
NAME:   evilmike_library_of_no_secrets
ORIENT: southeast
DEPTH:  D:11-, Depths
TAGS:   no_pool_fixup no_monster_gen
TILE:   c = DNGN_STONE_WALL_BLUE
COLOUR: c" = lightblue
FTILE:  .+def12Ubc|* = FLOOR_CRYSTAL_SQUARES
FTILE:  "_ = FLOOR_MARBLE
KFEAT:  _ = altar_sif_muna
SUBST:  Y = U, U = U:4 V:1 T:1
: if you.absdepth() < 16 then
MONS:   w:20 orc wizard / w:40 deep elf mage / w:30 naga mage / w:10 vampire mage
MONS:   w:2 orc sorcerer / w:1 deep elf sorcerer
: elseif you.absdepth() < 21 then
MONS:   w:5 orc wizard / w:25 deep elf mage / w:15 naga mage / w:25 wizard / \
        w:10 vampire mage / w:20 ogre mage
MONS:   w:25 orc sorcerer / w:50 deep elf sorcerer / w:25 lich
: elseif you.absdepth() < 26 then
MONS:   w:1 orc wizard / w:5 deep elf mage / w:4 naga mage / w:30 wizard / \
        w:4 vampire mage / w:55 ogre mage / w:1 lich
MONS:   w:50 deep elf sorcerer / w:35 lich / w:15 ancient lich
: else
MONS:   w:5 deep elf mage / w:2 naga mage / w:30 wizard / \
        w:3 vampire mage / w:55 ogre mage / w:5 lich
MONS:   w:25 deep elf sorcerer / w:50 lich / w:25 ancient lich
: end
ITEM:   any book / w:20 any randbook
ITEM:   w:18 any magical staff / any rod w:5 / any jewellery
ITEM:   any randbook / gold
SUBST:  " = .
MAP
                xxxxxxxxxxxxxxxxxxxxxxxxx
                xcccccccccccccccccccccccc
                xccccccccwwwwwwcccccccccc
                xccccU............Ucccccc
                xcwwc..b..b..b..b..cccccc
                xc|*+..b..b..b..b..cccccc
                xc||c..2..1..1..1..cccccc
                xc|*+..b..b..b..b..cccccc
  xxxxxxxxxxxxxxxcwwc..b..b..b..b..cccccc
  xccccccccccccccccccU............Ucccccc
  xcccccccccccccccccccccccc++cccccccccccc
  xcccccccccccccccccccccccw..wccccccccccc
  xcccccf...cc...fccccccccw..wccccccccccc
  xccccc.bb.cc.bb.ccccccccc++cccccccccccc
  xccccc..1.cc.1..cccwwwwwwWWwwwcccwwwwwc
  xcccccccc+cc+ccccccwbb..1..bbwcccdd.eec
''cccc..............cwb...U...bwccccc+ccc
'Yc.1c..bbbb..bbbb1.cw..U"""U..wccc.....c
@'+..c..............cw.."""""..wccc..b.2c
@Yc..+..bbbb1.bbbb1.+W.U""_""U2W+.+.bUb1c
@'+..c..............cw.."""""..wccc..b.2c
'Yc.1c..bbbb..bbbb1.cw..U"""U..wccc.....c
''cccc..............cwb...U...bwccccc+ccc
  xcccccccc+cc+ccccccwbb..1..bbwcccee.ddc
  xccccc..c....c..cccwwwwwwwwwwwcccwwwwwc
  xcccccU.+1bb1+.Uccccccccccccccccccccccc
  xcccccf.c....c.fccccccccccccccccccccccc
  xcccccccccccccccccccccccccccccccccccccc
  xcccccccccccccccccccccccccccccccccccccc
  xcccccccccccccccccccccccccccccccccccccc
ENDMAP

################################################################################
# Okawaru armoury
#
NAME:   evilmike_iron_armoury
ORIENT: southeast
TAGS:   no_monster_gen no_item_gen
DEPTH:  D:11-, Depths
KFEAT:  _ = altar_okawaru
SUBST:  = = nv
FTILE:  '+Gvc12345_defghijk = floor_hall
: if you.absdepth() < 16 then
MONS:   deep elf knight w:1 / deep elf archer w:1 / orc warrior
MONS:   w:20 orc warrior / centaur warrior
MONS:   orc warrior
MONS:   w:8 patrolling orc knight / w:2 patrolling orc warlord
: elseif you.absdepth() < 21 then
MONS:   w:1 deep elf knight / w:1 deep elf archer / w:8 orc warrior / \
        w:4 centaur warrior
MONS:   w:12 orc knight / w:3 deep elf knight / w:3 deep elf archer / \
        w:4 centaur warrior / w:2 yaktaur captain
MONS:   orc knight
KMONS:  4 = patrolling Wiglaf, patrolling orc warlord
: elseif you.absdepth() < 26 then
MONS:   w:1 deep elf knight / w:1 deep elf archer / w:8 orc knight / \
        w:4 centaur warrior / w:2 yaktaur captain
MONS:   w:8 orc knight / w:1 deep elf knight / w:1 deep elf archer / \
        w:6 yaktaur captain
MONS:   orc warlord
KMONS:  4 = patrolling Wiglaf, patrolling deep elf blademaster
: else
MONS:   w:1 deep elf knight / w:1 deep elf archer / w:8 orc knight / \
        w:6 yaktaur captain
MONS:   w:4 orc warlord / w:1 deep elf blademaster / w:3 deep elf master archer
MONS:   deep elf blademaster
KMONS:  4 = patrolling Wiglaf, patrolling draconian knight
: end
SUBST:  5 = 2221'
# Lots of loot in this one. In true okawaru fashion, most of it is junk!
ITEM:   any armour, any armour good_item / acquire armour
ITEM:   any weapon, any weapon good_item / acquire weapon
ITEM:   arrow, bolt, sling bullet, gold
SHUFFLE: df, hi, jk, eg
NSUBST: d = 1:e / *:d
NSUBST: f = 1:g / *:f
SUBST:  ' = .
MAP
.................................x
.....v...........................x
.......vvvvvvvvvvvvvv......wwww..x
.....vvvvdd'ddvff'ffvvv.....www..x
.....viivdd'ddvff'ffvvvvv....ww..x
.....viivvv+vvvvv+vvvkkvvvv...w..x
.....v''+''5'''1'5''vkkveevv.....x
.....viiv'cc'''''cc'+''vv+vvv....x
..v.vviiv'ccc'''ccc'vkkv'5''vv...x
....vvvvv''cc'''cc'2vvvv'''''vv..x
..v.v'1vG'''''''5'''Gv''''''3'vv.x
....+''+''''''''2'''1+'''''4_''=.x
..v.v'1vG'''''''5'''Gv''''''3'vv.x
....vvvvv''cc'''cc'2vvvv'''''vv..x
..v.vvhhv'ccc'''ccc'vjjv'5''vv...x
.....vhhv'cc'''''cc'+''vv+vvv....x
.....v''+''5'''1'5''vjjvggvv.....x
.....vhhvvv+vvvvv+vvvjjvvvv...w..x
.....vhhvff'ffvdd'ddvvvvv....ww..x
.....vvvvff'ffvdd'ddvvv.....www..x
.......vvvvvvvvvvvvvv......wwww..x
.....v...........................x
.................................x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Vehumet Hall
#
NAME:   evilmike_halls_of_destruction
ORIENT: northeast
DEPTH:  Depths
TAGS:   no_monster_gen
COLOUR: v'4! = red
COLOUR: = = red
TILE:   v = DNGN_STONE_WALL_RED
FTILE:  v'4! = FLOOR_PEBBLE_RED
FTILE:  = = FLOOR_PEBBLE_RED
FTILE:  G1BC = FLOOR_VAULT
MARKER: ! = lua:fog_machine { \
                pow_max = 10, delay_min = 10, delay_max = 20, \
                size = 8, size_buildup_amnt = 5, \
                size_buildup_time = 10, cloud_type = "flame" \
            }
KFEAT:  _ = altar_vehumet
## Made to be always the same hd for consistency. - HangedMan
MONS:   statue hd:12 spells:orb_of_destruction.62.magical \
        tile:mons_statue_mage col:vehumet name:conjurer n_adj
: if you.absdepth() < 22 then
MONS:   w:6 deep elf mage / w:2 wizard / w:1 spriggan air mage / w:1 deep elf annihilator
MONS:   w:3 deep elf annihilator / w:1 deep elf mage
KMONS:  5 = nikola, deep elf annihilator
: else
MONS:   w:4 deep elf mage / w:2 wizard / w:2 spriggan air mage / w:2 deep elf annihilator
MONS:   w:2 deep elf annihilator / w:1 draconian annihilator
KMONS:  5 = frederick, boris, draconian annihilator
: end
MONS:   fire vortex
NSUBST: G = 1:1 / 1:1G / *:G
NSUBST: A = 4:2 / 2=2*. / 1:|
NSUBST: B = 1:1 / *:G
NSUBST: C = 1:1 / *:G
SUBST:  v = c, '! = .
MAP
xccccccccccccccccccccccccccccc
xcG....+....Ac...A..GccGccGccc
xccc+ccc.....+......ccc3cc3ccc
xc.....ccc+cccccccc+cc*||||*cc
xc...A.+..A..+.Ac...cG**CC**Gc
xc+ccccccccccc..cA..cc*....*cc
xc.cGcGcGc1ccc+ccc+ccG......Gc
xc..........cc..A+..cc......cc
xc2.........Gcccccc.cccc++cccc
ccccGcGcc...cccvvvc.+........c
ccGcccccG...Gccv4vccc...ccc..c
...2ccccc...vvvv'vvvv....ccc.c
@.......+...=''4!4''=...5_cc2c
...2ccccc...vvvv'vvvv....ccc.c
ccGcccccG...Gccv4vccc...ccc..c
ccccGcGcc...cccvvvc.+........c
xcG2........Gcccccc2cc++cc++cc
xcc.........cccc1cc.c...cc...c
xcG..cGcGc1cccG.....c...cc...c
xcc..cccccccccc...ccc%%%cc$$$c
xcG.........ccG...Gcc*B*cc$B$c
xcc.......12ccc...ccc***cc$$$c
xcG...........+...Gccc3cccc3cc
xccccccccccccccccccccccccccccc
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

################################################################################
# Kikubaaqudgha Temple
#
NAME:   evilmike_negative_zone
ORIENT: southeast
DEPTH:  Depths
TAGS:   no_monster_gen
#
# gives a 50% chance of there being at least one shortcut in this vault
SUBST:  = = c:8 +:1
TILE:   c = WALL_BRICK_GRAY
# maybe use something darker?
#
# 66% chance that there are no one width corridors in the inner part.
SUBST:  B : b..
TILE:   b = WALL_STONE_GRAY
COLOUR: b = white
SUBST:  b = c
#
KFEAT:  _ = altar_kikubaaqudgha
: if you.absdepth() < 19 then
MONS:   zombie / skeleton / w:5 simulacrum
MONS:   necromancer, deep elf death mage, \
        w:5 deep elf death mage / nothing
MONS:   w:4 soul eater / w:1 reaper, reaper, reaper / nothing
: elseif you.absdepth() < 24 then
MONS:   zombie / skeleton / w:5 simulacrum
MONS:   necromancer w:15 / w:5 deep elf death mage, lich, \
        lich / deep elf death mage / nothing
MONS:   soul eater / w:5 reaper, reaper, shadow fiend / w:5 reaper
: else
MONS:   zombie / skeleton / w:5 simulacrum
MONS:   necromancer / deep elf death mage, \
        w:15 lich / w:5 ancient lich / deep elf death mage, lich / ancient lich
MONS:   reaper / soul eater, reaper, shadow fiend
: end
MARKER: V = lua:fog_machine { cloud_type = "foul pestilence", \
            pow_min = 8, pow_max = 12, delay_min = 20, delay_max = 30, \
            size = 1, walk_dist = 1, spread_rate= 33 }
SUBST:  % = %*|$
SUBST:  A = 1.
MAP
xxxx......@@@......xxxxxxxxxxxxxxxxxxxxxxxxx
xccc......1.1......ccccccccccccccccccccccccc
xccc..b..1c+c1..b..c%%c...5...cccccccccccccc
xccc.....cc.cc.....=%%+...V...+..cc.cccccccc
xccc..b.2c...c2.b..c%%c.......cc....ccc1>1cc
xccc.....c...c.....cccc.......cc.bb.ccc.c.cc
xccc=ccccc...ccccccccccccc+ccccc.BB.ccc.c.cc
xcb$$bc.........cb$$bc....5....c....ccc.c.cc
xc1..Ac.Bb...bB.cA..1c.b.ccc.b.c5BB5cc....cc
xcb..bc.bb...bb.cb..bc.b.ccc.b.c.bb.cc.c.ccc
xcA...+.........+...A=..2...2..=.6..cc.c.ccc
xcb..bc.bb...bb.cb..bccccc.ccccc.cc..+.c.ccc
xc1..Ac.Bb...bB.cA..1=AAAc.cAAAccccccccc+ccc
xcb$$bc...3_4...cb$$bcA$Ac.cA$Accccccc.....c
xccc=ccccc...cccccccccA2Ac+cA2Accccccc%6.6%c
xcc%%2ccccc+ccccccccccc+cc.cc+ccAAAccc%%7%%c
xcc%%.+..........A.1.A.........+2$Accc%%%%%c
xcc2..c...........A.12A........cAAAccc%%%%%c
xccccccccccccccccccccccccccccccccccccccccccc
ENDMAP

###############################################################################
NAME:    grunt_house_of_mirrors
TAGS:    no_tele_into
DEPTH:   Depths, !Depths:$
ORIENT:  north
SHUFFLE: AB / BA / BB, CD / DC / DD, EF / FE / FF
SHUFFLE: GH / HG / HH, IJ / JI / JJ, KL / LK / LL
SHUFFLE: MN / NM / NN, OP / PO / PP, QR / RQ / RR
SUBST:   ACEGIKMOQ = b
SUBST:   BDFHJLNPR = .
{{
  -- This is a list of monsters in D that have attacks that can bounce off of
  -- crystal walls (fire, cold, lightning), weighted by their normal dungeon
  -- weight functions!
  local function peak(depth, low, high, chance)
    if depth < low or depth > high then return 0 end
    local len = high - low + 2
    return math.floor(chance * (len - math.abs(low + high - 2 * depth)) / len)
  end
  local function semi(depth, low, high, chance)
    if depth < low or depth > high then return 0 end
    local len = high - low
    if len == 0 then len = 2 else len = len * 2 end
    return math.floor(chance * (len - math.abs(low + high - 2 * depth)) / len)
  end
  local function flat(depth, low, high, chance)
    if depth < low or depth > high then return 0 end
    return chance
  end

  local function add_mons(mons, newmons, chance)
      if chance <= 0 then return mons end
      if mons == nil then return newmons .. " w:" .. chance end
      return mons .. " / " .. newmons .. " w:" .. chance
  end

  local function monslist(depth)
    local mons = nil
    mons = add_mons(mons, "mottled dragon", semi(depth, 11, 21, 334))
    mons = add_mons(mons, "efreet", peak(depth, 13, 19, 52))
    mons = add_mons(mons, "wizard", peak(depth, 14, 22, 89))
    mons = add_mons(mons, "fire dragon", peak(depth, 14, 24, 192))
    mons = add_mons(mons, "ice dragon", semi(depth, 17, 25, 136))
    mons = add_mons(mons, "necromancer band", peak(depth, 18, 24, 52))
    mons = add_mons(mons, "orc sorcerer", peak(depth, 21, 27, 25))
    mons = add_mons(mons, "frost giant", peak(depth, 22, 30, 136))
    mons = add_mons(mons, "fire giant", peak(depth, 22, 30, 136))
    mons = add_mons(mons, "lich", peak(depth, 22, 30, 66))
    mons = add_mons(mons, "hell knight band", peak(depth, 23, 29, 25))
    mons = add_mons(mons, "storm dragon", peak(depth, 23, 31, 89))
    mons = add_mons(mons, "deep elf mage", flat(depth, 26, 28, 6))
    mons = add_mons(mons, "golden dragon", peak(depth, 29, 33, 8))
    mons = add_mons(mons, "titan", peak(depth, 28, 34, 25))
    mons = add_mons(mons, "deep elf annihilator", peak(depth, 30, 31, 4))
    if mons == nil then return "nothing" end
    return mons
  end

  local ninedepth = you.absdepth() + 5
  if ninedepth > 31 then ninedepth = 31 end

  kmons("0 = " .. monslist(you.absdepth()))
  kmons("9 = " .. monslist(ninedepth))
  kmons("8 = " .. monslist(31)) -- this cap's always hit at D:14-
}}
KFEAT: 123456 = .
validate {{
  return has_exit_from_glyph('1')
         and has_exit_from_glyph('2')
         and has_exit_from_glyph('3')
         and has_exit_from_glyph('4')
         and has_exit_from_glyph('5')
         and has_exit_from_glyph('6')
}}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxx
xbbbbbbbbbbbbbbbbbbbbbbbx
xb%*%bbb*|*bbb*|*bbb%*%bx
xb.9.bbb.8.bbb.8.bbb.9.bx
xbb1bbbbb.bbbbb.bbbbb3bbx
xbbb.b.bQb.b2b.bRb.b.bbbx
xbbbb.9.bbb.9.bbb.9.bbbbx
xbBbbb.b.bQb.bRb.b.bbbGbx
xbBBbbbbb.bbbbb.bbbbbGGbx
xbBBBbbb.0.bbb.0.bbbGGGbx
xbBBbbbbb.bbbbb.bbbbbGGbx
xbBbBb.bCbDb.bEbFb.bGbGbx
xbbbb.0.bbb.0.bbb.0.bbbbx
xbBbAb.bDbCb5bFbEb.bHbGbx
xb..bbbbb.bbbbb.bbbbb..bx
xb.04bbb.0.bbb.0.bbb60.bx
xb..bbbbb.bbbbb.bbbbb..bx
xbIbJb.bKbLb.bMbNb.bObPbx
xbbbb.0.bbb.0.bbb.0.bbbbx
xbJbIb.bLbKb.bNbMb.bPbObx
xbIIbbbbb.bbbbb.bbbbbPPbx
xbIIIbbb...bbb...bbbPPPbx
xbIIIbbb...bbb...bbbPPPbx
xbbbbbbb...bbb...bbbbbbbx
xxxxxxxx.@.xxx.@.xxxxxxxx
ENDMAP

# Based off Zaba's beehive_diamond.
# Hopefully the changes for it to fit in D make it more interesting than Hive.
NAME:    hangedman_what_comes_after_a
TAGS:    no_monster_gen no_item_gen
DEPTH:   D:12-
ORIENT:  north
MONS:    killer bee, queen bee, oklob plant
KFEAT:   X = x
ITEM:    royal jelly
KITEM:   | = |
SUBST:   A = X:3 .:2, z = x.., ' = .:460 01d%
: if you.absdepth() > 14 then
KFEAT:   | = <
SUBST:   " = 1
: else
KFEAT:   | = >
SUBST:   8 = 9, " = '
: end
TILE:    X = wall_wax
COLOUR:  X = yellow
MAP
             xxxxxxxxxxxxxxx
             xxxxxcccccxxxxx
             xxxccc*e*cccxxx
              xxc3%e|e%3cxx
               xcc3*e*3ccx
               xxcccmcccxx
             xxxxx18281xxxxx
           xxxxxd1911191dxxxxx
          xxxxx'"X"eAe"X"'xxxxx
         xxxx'''XXX"""XXX'''xxxx
        xxxx''X''X''X''X''X''xxxx
      xxxx'''XXX'''XXX'''XXX'''xxxx
     xxxx''X'1X''X''X''X''X1'X''xxxx
    xxx'''XXX90'XXX'0'XXX'09XXX'''xxx
 xxxxx.'X''X'1X1'X''X''X'1X1'X''X'.xxxxx
xxzx...XXX'''XXX'''XXX'''XXX'''XXX...xzxx
x.x..A.'X''X''X''X'1X1'X''X''X''X'.A..x.x
x.x.AAA'''XXX'0'XXX'9'XXX'0'XXX'''AAA.x.x
.x...A.'X'1X''X''X'1X1'X''X''X1'X'.A...x.
x.x....XXX010XXX'''XXX'''XXX010XXX....x.x
x.x..A.'X'1X1'X''X''X''X''X'1X1'X'.A..x.x
.x..AAA'''XXX'''XXX'''XXX'''XXX'''AAA..x.
x.x..A.'A''X''X'1X''X''X1'X''X''A'.A..x.x
x.x....AAA'''XXX010XXX010XXX'''AAA....x.x
.x.x....A''A''X'1X1'X'1X1'X''A''A....x.x.
x.xzx.....AAA'''XXX'''XXX'''AAA.....xzx.x
.x.xzx.....A''A''X''X''X''A''A.....xzx.x.
 .x.x.x.x....AAA...XXX...AAA....x.x.x.x.
  .xzx.x.x....A..A..X..A..A....x.x.xzx.
   .xzx.xzx...................xzx.xzx.
    .....xzxx.xx.xx.x.xx.xx.xxzx.....
         .x..x.zx..xzx..xz.x..x.
          .xx.xx.xx.x.xx.xx.xx.
ENDMAP

###############################################################################

NAME:   kennysheep_demon_jail
ORIENT: north
TAGS:   no_monster_gen
DEPTH:  Depths:2-
MONS:   common demon, hell knight
MONS:   shadow demon / green death / blizzard demon / balrug / cacodemon / \
        hell beast / hellion / reaper / lorocyproca / tormentor
NSUBST: . = 7:1 / 7:2
FTILE:  .|#+G123- = floor_pebble_darkgray
KPROP:  1Y = no_tele_into
KFEAT:  # = iron_grate
MAP
xxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxvvvvvvvvxxxxxx
vvvvvvvvvxxv||v.3.vxxxxxx
v1v1v1v1vxxv||+...vxxxxxx
v#v#v#v#vvvvvvv...vxxxxxx
v.........llllv...vvvvvvv
v#v#v#v#vl..l.+...+.....v
v1v1v1v1vl....vvvvv.GlG.v
vvvvvvvvv.l.........lYl.v
v1v1v1v1v.ll........GlG.v
v#v#v#v#v.lll.v---v.....v
v..........l..v   vvvvvvv
v#v#v#v#vvvvvvv
v1v1v1v1v
vvvvvvvvv
ENDMAP

NAME:   kennysheep_seaside_town
ORIENT: northeast
TAGS:   no_monster_gen
DEPTH:  D:7-14
KFEAT:  W = open_sea
MONS:   ogre, orc warrior, gnoll shaman, centaur, big kobold
KMONS:  8 = orc warrior band
KMONS:  9 = gnoll shaman band
KMONS:  p = plant
KMONS:  P = fungus
FTILE: ' = floor_sand
FTILE: .pt89 = floor_dirt
NSUBST: H = 3:* / 7:1
KFEAT:  H = floor
NSUBST: . = 2:9
SUBST:  p = p...
NSUBST: . = 5:p
SUBST:  1 = 12345
NSUBST: q = 3:1 / 6:P / *:.
MARKER: F = lua:fog_machine { cloud_type = "flame", pow_min = 100, pow_max = 100, \
                 delay = 10, size = 1, walk_dist = 0, excl_rad = -1 }
MAP
xxxxWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW
xxxxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW
xxxxwwwwwwwwww'wwwwwww''wwwww'wwwwwwW
xxxxwww'wwwww'''w''''ww''''''''wwwwwW
xxxxwwww'''''..'''.''''''..''''wwwwwW
xxxxwwww'...cccc...cccc.......''wwwwW
xxxxwwww''..cHHc...cHHc........'wwwwW
xxxxw''''...cHHc...cHH+...cccc.''wwwW
xxxx''cccc..cc+c...cccc...cHHc..'wwwW
xxxx..cHHc................+HHc..''wwW
xxxx..cHH+................cccc...'wwW
xxxx..cccc.......ccccc...........'wwW
xxxx.......8..ccccHHHc..........''wwW
xxxxtp.p.pt..tc>zcHHH+..........'wwwW
xxxxt.p.p.ptxxczz+HHH+..........'wwwW
xxxxtpppptxxx!!!ccccccxxxppp....'wwwW
xxxxttptpxx!!!!xxxxx xxxppppppp.''wwW
xcccc+tptx!!!xxxxxqxxxx.pptptppp''wwW
xcHHHctttx!!xxxxxxxqqqqqxtptptpcmcwwW
xcHHHcxxxx!xxxxxxxqxxxxxxxttttccHccwW
xcHHHcx!!!!!xxxxxxqxqxxxxxptppmHFHmwW
xccccc!!!x!xxxxxxqqxqqqqxxtpptc+HccwW
xxxxx!!xxxxxxxxxxqxqqxxqxxpppppcmcwwW
xxxxxx!!xxxxxxxxxxxxqxxqqqxxxxxxwwwwW
xxxxxxx@@xxxxxxxxxxxxxxxxxx    xxxxxx
ENDMAP

NAME:   kennysheep_kobold_camp
ORIENT: north
TAGS:   no_monster_gen no_trap_gen
DEPTH:  D:4-9
MONS:   big kobold, kobold
MARKER:  F = lua:fog_machine { cloud_type = "flame", pow_min = 100, \
                               pow_max = 100, delay = 10, size = 1, \
                               walk_dist = 0, excl_rad = 0 }
KMONS:  p = plant
NSUBST: . = 15:2 / *:.
SUBST:  t = tp`
NSUBST: ' = 5:C / 5:2
: dgn.delayed_decay(_G, 'C', 'human corpse')
KPROP:  ` = no_tele_into
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxttttttttxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxtttttttttttttxxxxxxxxx
       xxxxtttttttttttttttttttttxx
       xxtttttttttt*t..*t.ttttttxx
     xxxtttttttttt.''1'......ttttxxx
    xxtttttttt....''''''.....ttttttxx
   xxtttttt.......''FF'''.....ttttttxx
  xxtttttttt.....'''FF'''.....ttttttt@
 xxttxxxxtttt.....''''''.....ttttxxxxx
xxttxx  xxtttt.....''''.....tttxxx
@txxx    xtttttt.........tttttxx
xxx      xxxtttttttt....ttttttx
           xtttttttttt.ttttxxxx
           xxxxxxtttxt.ttxxx
                xttxxtt.ttxx
                xtttxxtt.ttxx
                xxttxxxtt.ttx
                 xtttxxxtt.txx
                 xxttxxxxt.ttx
                  xxxttxxtt.txx
                    xttxxxxt.tx
                     @@    @@@
ENDMAP

NAME:   kennysheep_goblin_castle
TAGS:   no_monster_gen no_pool_fixup
ORIENT: northwest
DEPTH:  D:2-4
## this gives a chance for Ijyb and Robin to appear as the boss if they
## haven't been placed, selecting from who is available.
{{
  -- Default for when we're only map validating.
  if not crawl.game_started() then
    kmons("O = Ijyb / Robin / hobgoblin w:1")
    subst("B = 1")
    return
  end

  -- possible hobgoblin bosses and their weights
  bossw = {hobgoblin = 1, Ijyb = 10, Robin = 10}
  bosses = {hobgoblin = 1}

  -- Find out which hobgoblin uniques we can use
  if not you.uniques("Ijyb") then
    bosses["Ijyb"] = bossw["Ijyb"]
  end
  if not you.uniques("Robin") then
    bosses["Robin"] = bossw["Robin"]
  end

  -- Choose a boss, and remove the supporting goblins if we select Robin, since
  -- she has her own band.
  boss = crawl.random_element(bosses)
  if boss == "Robin" then
    subst("B = .")
    boss = "Robin band"
  else
    subst("B = 1")
  end
  kmons("O = " .. boss)
}}
MONS:   goblin, goblin ; hunting sling . stone, jackal
NSUBST: . = 1:0 / 3:3 / *:.
MAP
xxxxxxxxxxxxxxxxxxx
x...+....B..x%%*xww
x...+......O+%%%xww
x...x....B..xxxxxww
x...xxxxxxxxxx...ww
x............x...ww
x.....1.1....x.2.ww
xx+xx.....xx+xx..ww
x...x.....x...x..ww
x...x.....x...x..ww
x...x.....x...x..ww
xx+xx.....xx+xx..ww
x.2.x.....x.2....ww
x...x.....x.....www
xwwwx.....xwwwwwww
xwwwxx+++xxwwwwww
  wwww...wwww
    ww@@@ww
ENDMAP

NAME:   kennysheep_tiny_hell
TAGS:   no_monster_gen
ORIENT: centre
DEPTH:  D:13-14
WEIGHT: 5
FTILE:  *|c.12${} = floor_infernal_blank
TILE:   c = dngn_stone_dark
SUBST:  *=*|
MONS:   ice devil w:1 / hellwing w:3 / rust devil w:1 / orange demon w:1 /\
        red devil w:1 / demonic crawler / sixfirhy w:1
MONS:   lesser demon, river rat / giant cockroach
NSUBST: . = 6:3 / 6:! / 56:' / .
KPROP:  !' = bloody
: dgn.delayed_decay(_G, '!', 'human corpse / human skeleton')
MAP
              cccccccccc
              c...$$...c
              c...**...c
              c....1...c
              c2..2..2.c
              c........c
              c........c
              c........c
              c........c
              c....2...c
             cc........cc
            cc..........cc
           cc............cc
          cc..............cc
ccccccccccc................ccccccccccc
c................................2...c
c...2................................c
c....................................c
c$*1.....2........{.........2....2.*$c
c$*.2..............}..............1*$c
c....................................c
c................................2...c
c...2................................c
ccccccccccc................ccccccccccc
          cc..............cc
           cc............cc
            cc..........cc
             cc........cc
              c...2....c
              c........c
              c........c
              c........c
              c........c
              c.2..2..2c
              c...1....c
              c...**...c
              c...$$...c
              cccccccccc
ENDMAP

NAME:   kennysheep_housing_project
ORIENT: north
TAGS:   no_monster_gen
DEPTH:  D:6-9
MONS:   kobold / hound w:5 / gnoll / hobgoblin, \
        big kobold / orc warrior w:5 / ogre / gnoll sergeant, \
        orc priest / orc wizard / gnoll shaman / centaur
NSUBST: . = 4:2 / 20:1 / 5:3
NSUBST: . = 8:% / 2:* / 10:$
FTILE: 'pt = floor_grass
KMONS:  p = plant
SUBST:  !':.
MAP
xxxxccccccccccccccccccccccccccccxxxxx
''''m.......+..............+...m'''''
''''m.......+..............+...m'''''
'pptc.......cccc++cccccccc+c...cppt''
'tptc.......c..........c...c...ctpp''
ppptc.......c..........c...c...ctpt''
'pttc.......c..........c.>.ccccctpp''
'pptc.......c..........c...cptttpt'''
'pttc.......ccccmmcccccc+++cppppp'''
 'ttccmmcccccttt''ptttt'!1!tppptp'''
  ''tt''ttt'tptp''pptpp'!!!'ptp'''''
  ptpt'''ptp'p'p''ptptp'!!!'p'p''''
  ''p''''p'p''''''p'p'''!!!'''''''
    ''''''''''''''''''''!!!'''
         ''''  '  ''   '!!!'
                        @@@
ENDMAP

NAME: cheibrodos_hippocracy
DEPTH: D:12-15
TAGS: no_item_gen no_monster_gen no_pool_fixup
ORIENT: north
MONS: giant orange brain, small abomination, giant eyeball, demonic crawler
ITEM: randbook
NSUBST: ' = 3:4 / *:.
NSUBST: " = 4:0 / 1:9 / *:.
NSUBST: ? = 1:9 / *:0
MAP
      xxxxxxxxxxxxxxxxxxxxxxx
     xxxxxxxxxxxxxxxxxxxxxxxxx
    xxxwwwwwxxxxxxxxx......xxxx
  xxxxwxxxxx...xxxx...xxxx..xxx
 xxxwwxx.........x..xxx..xx..xx
xxxwxxx............xx.....xx.xx
xxwxx*...?.?......xx..xx..2x.xx
xxwx%d....1.......x..xxW22Wx.xx
xxwx**...?.?.....xx..xxWWWxx.xx
xxwxx%..........xx....xxwxx..xx
xxxwxxxx......xxxw.xx..xxx..xxx
 xxxwwwxxxxxxxxwwww'xx.....xxx
  xxxxxwwwwwwwwxx.w'xxxxxxxxx
   xxxxxxxx4.xxxx.w'xxxxxxxx
   xx"m"xxx*%xxxxw'wxx
  xx"m""".xmxxxxx'wxxx
  xx3m"""..xxxx''w'xx
  xx"m"""..xxx'ww'xx
   xx"m"xx.xxx'w'xx
    xxxxxx.xx'ww'xx
        xx.xxx'w'xx
         xx.xx'ww'xx
         xx.xx'ww'xx
         xx.x.xw'xx
          xx.xxx'xxx
           xxxxxw.xx
              xxw.xx
              xx.xxx
              xx@xxx
ENDMAP

NAME:    kennysheep_kobold_grotto
DEPTH:   D:13-, !D:$
TAGS:    no_monster_gen
ORIENT:  northwest
MONS:    big kobold, patrolling kobold demonologist, kobold, plant
MONS:    lesser demon, obsidian statue
KMONS:   g = bush
SHUFFLE: !'
NSUBST:  ! = 3:C / 4:- / 2:5 / *:.
NSUBST:  ' = 2:C / *:.
NSUBST:  . = 10:3 / 4:5
SUBST:  .z = .:200 W:35 4:20
: dgn.delayed_decay(_G, 'C', 'halfling corpse / halfling skeleton')
KPROP:  -C = bloody
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx!!xxxx'''xxxx'''xxxxxx
xxxx!!!xxx''''xx'''''xxxxx
xxxxxgxxxxggxxxxxggxxxx!!x
xxxxx................xx!!x
xxxx...w..............g!xx
x'x...www.............xxx
x'g....w.........w...xx
x'g..........x1.wWw.1xx
xxx....w....xx.wW6Ww..x
xx....wGw...xx..wWw..xx
xxx.w1.w.1wxxx...w...xx
xxxxww.1.wwxx.1.....1xx
 xxxx2***2xxx.......xxx
 xxxx**|**1xxxx.....xxx
  xxxxxxxxxxxxx.....xxx
    xxxxxxxxxxxx....xx
              xxx@.xx
ENDMAP
