lua {{
require('dlua/vault.lua')

MAX_RANDOM_SHOPS = 5

-- If a level gets shops, how many there are.
-- Just one most of the time; expected value is 1.42.
function num_shops()
    if crawl.x_chance_in_y(5, 6) then
        return 1
    else
        return crawl.random_range(2, MAX_RANDOM_SHOPS)
    end
end

function ks_shop_vault(e, glyphs)
  if e.is_validating() then
    e.subst('O = B')
    return
  end

  local is_shop = false
  local is_altar = false

  local params = dgn.map_parameters()
  if params ~= nil then
    local tag = unpack(params)
    if tag ~= nil then
      if tag == "shop" then
        is_shop = true
      elseif string.find(tag, "temple_overflow_generic_") ~= nil then
        is_altar = true
      end
    end
  end

  if is_shop then
    e.kfeat('O = any shop')
  elseif is_altar then
    e.subst('O = B')
  else
    e.tags('extra decor')
    e.subst('O = ' .. glyphs)
  end
end
}}

# XXX: Don't change the name of this vault!
# Gozag code uses it to find out where shops can normally place.
NAME: serial_shops
TAGS: allow_dup extra luniq
DEPTH: D:4-, Depths, Orc, Elf, Shoals, Snake, Vaults
CHANCE: 20% (D)
CHANCE: 20% (Depths)
CHANCE: 30% (Orc)
CHANCE: 40% (Elf, Vaults)
CHANCE: 20% (Shoals, Snake)
{{
hook("post_place", function()
    local num = num_shops()

    if you.absdepth() > 10 and you.absdepth() <= 27
      and crawl.one_chance_in(30 - (you.absdepth() - 1))
    then
        dgn.place_maps { tag = "bzr_entry", count = 1 }
        num = num - 1
    end

    dgn.place_maps { tag = "shop", count = num }
end)
}}
MAP
ENDMAP

# Shop vaults: Tagged shop, may only contain a single shop.
NAME:   shop
TAGS:   allow_dup extra shop transparent
WEIGHT: 100
KFEAT:  O = any shop
MAP
O
ENDMAP

NAME:   shop_fountains
TAGS:   allow_dup extra shop transparent
KFEAT:  O = any shop
MAP
T.T
.O.
T.T
ENDMAP

NAME:   shop_statue
TAGS:   allow_dup extra shop no_tele_into
KFEAT:  O = any shop
MAP
@G..O..G@
ENDMAP

NAME:   shop_coins
TAGS:   allow_dup extra shop transparent
WEIGHT: 3
KFEAT:  O = any shop
MAP
$$$
$O$
$$$
ENDMAP

NAME:   shop_scrolls
TAGS:   allow_dup extra shop transparent
WEIGHT: 1
KFEAT:  O = scroll shop
ITEM:   nothing w:30 / scroll of noise w:2 /\
        scroll of random uselessness w:8
MAP
ddd
dOd
ddd
ENDMAP

NAME: nicolae_shop_behind_doors
TAGS: allow_dup extra shop transparent
KFEAT: O = any shop
MAP
x+x
+O+
x+x
ENDMAP

NAME: nicolae_shop_crystal
TAGS: allow_dup extra shop transparent
KFEAT: O = any shop
MAP
b.b
.O.
b.b
ENDMAP

NAME: nicolae_shop_statue_plus
TAGS: allow_dup extra shop no_tele_into
KFEAT: O = any shop
MAP
   @
   G
   .
@G.O.G@
   .
   G
   @
ENDMAP

NAME: nicolae_shop_stone
TAGS: allow_dup extra shop transparent
KFEAT: O = any shop
MAP
  c
 ...
c.O.c
 ...
  c
ENDMAP

NAME: nicolae_shop_trees
TAGS: allow_dup extra shop transparent
KFEAT: O = any shop
MAP
  t
t.O.t
  t
ENDMAP

NAME: nicolae_shop_waterfront
TAGS: allow_dup extra shop no_pool_fixup
KFEAT: O = any shop
SUBST: v = wW
MAP
 www
wwvvW
wwO.@
wwvvW
 www
ENDMAP

NAME: nicolae_shop_overgrown_old_shop
TAGS: allow_dup extra shop transparent
WEIGHT: 5
KFEAT: O = antiques shop / antique weapon shop w:5 / antique armour shop w:5
NSUBST: 1 = . / 1
MONS: plant / nothing w:3
MAP
.....
.111.
.1O1.
.111.
.....
ENDMAP

# A shop owner has copied a temple entry for their shop. Is nothing sacred?
# (minmay_temple_entry_statues_fountains)
NAME: nicolae_shop_temple_entry_statues_fountains
TAGS: allow_dup extra shop transparent
WEIGHT: 5
SUBST: T = TV
KFEAT: O = any shop
MAP
G.G.G
.T.T.
G.O.G
.T.T.
G.G.G
ENDMAP

NAME: nicolae_shop_window_shopping
TAGS: allow_dup extra shop
WEIGHT: 5
KFEAT: O = any shop
MAP
xxm.
+Om@
xxm.
ENDMAP

NAME: nicolae_shop_competitors_out_of_business
TAGS: allow_dup extra shop transparent
WEIGHT: 3
KFEAT: O = any shop
KFEAT: A = abandoned_shop
MAP
O.A
...
A.A
ENDMAP

####
# These three vaults were originally plain minivaults.
# They can still place as plain minivaults, in which case they have no
# shops.
NAME:  kennysheep_mini
TAGS:  shop temple_overflow_generic_1
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
: ks_shop_vault(_G, '{}CC')
MAP
cDc
FOF
c+c
ENDMAP

NAME:  kennysheep_mini_moat
TAGS:  shop temple_overflow_generic_1
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
SUBST: . : @
: ks_shop_vault(_G, '{}CC')
MAP
wwEww
wcDcw
HFOFH
wc+cw
ww@ww
ENDMAP

NAME:  kennysheep_little_box
TAGS:  shop temple_overflow_generic_1
DEPTH: D:4-, Depths, !D:$, !Depths:$
: ks_random_setup(_G)
: ks_shop_vault(_G, '{}C....')
MAP
cccDccc
cwwEwwc
cwt.twc
FH.O.HF
cwt.twc
cww.wwc
ccc+ccc
ENDMAP

NAME: nicolae_shop_scaring_up_business
TAGS: allow_dup extra shop
WEIGHT: 1
KFEAT: O = food shop
KMONS: O = patrolling hungry ghost
MAP
...
.O.
...
ENDMAP

#####################################################
# Custom venom shop Vault
NAME:    guppyfry_shop_venom
TAGS:    no_monster_gen
DEPTH:   D:4-, Depths
MONS:    ball python w:5 / adder / water moccasin, place:Snake:1
KFEAT:   v = general shop type:Serpentskin suffix:Sales count:7 ; \
         w:8 blowgun | w:8 needle ego:poisoned | w:4 needle ego:curare | \
         w:14 arrow ego:poisoned | w:14 tomahawk ego:poisoned | \
         w:3 ring of poison resistance | w:6 randbook disc:poison | \
         w:2 staff of poison | w:4 any weapon ego:venom | \
         w:4 any armour ego:poison_resistance | w:1 swamp dragon hide
SUBST:   ? = 2..
: if you.absdepth() < 13 then
SUBST:   2 = 1
: end
MAP
xxxxx
x???x
+?v?+
x???x
xxxxx
ENDMAP

#####################################################
# Custom earth shop Vault
NAME:    guppyfry_shop_earth
TAGS:    no_monster_gen
DEPTH:   D:11-, Depths
KMONS:   1 = gargoyle / basilisk / earth elemental / cyclops w:3
KMONS:   2 = stone giant / catoblepas / deep troll earth mage / boulder beetle / \
             iron dragon w:4
ITEM:    stone
KFEAT:   v = general shop type:Pet suffix:Rocks count:7 ; \
         w:15 stone | w:15 large rock | w:8 hunting sling | w:1 greatsling | \
         w:8 sling bullet | w:4 sling bullet ego:steel | \
         w:14 stone of tremors | w:7 crystal ball of energy | \
         w:4 wand of digging | w:5 randbook disc:earth | \
         w:2 book of the earth | w:2 book of geomancy | w:4 staff of earth | \
         w:4 wand of disintegration | w:1 crystal plate armour
SUBST:   ? = 2d.
: if you.absdepth() < 16 then
SUBST:   2 = 1
: end
MAP
.......
.xx+xx.
.x???x.
.m?v?m.
.x???x.
.xx+xx.
.......
ENDMAP

#####################################################
# Custom blood shop Vault
NAME:    guppyfry_shop_blood
TAGS:    no_monster_gen
DEPTH:   D:12-, Depths
KMONS:   1 = vampire / bat
KMONS:   2 = vampire / vampire mage / vampire knight
KFEAT:   v = general shop type:Blood suffix:Bar count:7 ; \
         w:70 potion of blood | w:10 any weapon ego:vampirism | \
         w:9 randbook disc:necromancy | w:1 book of annihilations
SUBST:   ? = 2.Y
: if you.in_branch("D") then
SUBST:   2 = 1
: end
MAP
xxxxx
x???x
+?v?+
x???x
xxxxx
ENDMAP

#####################################################
# Off-brand ammo
NAME:  nicolae_offbrand_ammo
DEPTH:  D:5-, Depths, Orc
WEIGHT: 1
TAGS:  no_hmirror no_vmirror
KFEAT: S = general shop type:Off-Brand suffix:Ammunition count:7 ; \
       tomahawk ego:flame | tomahawk ego:frost | tomahawk ego:chaos | \
       javelin ego:dispersal | javelin ego:exploding | javelin ego:chaos | \
       arrow ego:silver | arrow ego:steel | arrow ego:exploding | \
       arrow ego:penetration | arrow ego:chaos | bolt ego:exploding | \
       bolt ego:dispersal | bolt ego:chaos | \
       sling bullet ego:dispersal | sling bullet ego:chaos
MAP
.......
.lllll.
.lG.Gl.
.l.S.l.
.lG.Gl.
.ll.ll.
.......
ENDMAP

#####################################################
# Sells only evil items.
NAME: nicolae_evil_shop
WEIGHT: 2
TAGS: transparent
DEPTH: D:6-, Depths
SUBST: G : YlG
# I couldn't decide on just one alliterative name.
: local shopname = util.random_from({"type:Blasphemy suffix:Boutique",
:   "type:Evil suffix:Emporium", "type:Profane suffix:Products",
:   "type:Sacrilege suffix:Store", "type:Wicked suffix:Wares"})
: kfeat("s = general shop " .. shopname .. " ; \
:   potion of blood | any weapon ego:draining w:15 | any weapon ego:pain | \
:   any weapon ego:vampirism | any weapon ego:chaos w:1 | rod of clouds w:3 | \
:   demon blade w:5 | demon trident w:5 | wand of random effects w:3 | \
:   demon whip w:5 | scroll of torment | staff of death | \
:   lantern of shadows w:5 | book of necromancy | book of death | \
:   book of unlife | randbook disc:necromancy w:5 | necronomicon w:1 | \
:   grand grimoire w:1 | mace unrand:sceptre_of_torment w:1 | \
:   scythe unrand:scythe_of_curses w:1 | quarterstaff unrand:majin-bo w:1 | \
:   centaur barding unrand:black_knights_horse_barding w:1 | \
:   battleaxe unrand:obsidian_axe w:1 | arbalest unrand:hellfire w:1 | \
:   double sword unrand:sword_of_zonguldrok w:1")
KFEAT: a = altar_kikubaaqudgha / altar_yredelemnul / altar_makhleb / \
           altar_beogh w:1 / altar_lugonu w:1
MAP
...   ...
.G.a.s.G.
...   ...
ENDMAP

#####################################################
# Items related to the good gods thematically; healing, silver, holy wrath, etc.
NAME: nicolae_good_shop
WEIGHT: 2
DEPTH: D:6-, Depths
TAGS: no_monster_gen no_trap_gen
SHUFFLE: _s
KFEAT: s = general shop type:Hallowed suffix:Reliquary ; \
   potion of heal wounds | potion of curing | wand of heal wounds | \
   tomahawk ego:silver w:2 | javelin ego:silver w:2 | bolt ego:silver w:2 |\
   any weapon ego:holy_wrath | scroll of holy word |\
   sling bullet ego:silver w:2 | amulet of faith w:2 | potion of cure mutation | \
   ring of positive energy w:5 | any armour ego:positive_energy w:5
KFEAT: _ = altar_the_shining_one / altar_zin / altar_elyvilon
KPROP: 1 = no_tele_into
MONS: angel
MAP
nnn
n1n
nnn
._.
...
.s.
ENDMAP

#####################################################
# All bardings, all the time.
NAME: nicolae_barding_shop
DEPTH: D:8-, Depths, Snake, !Snake:$
WEIGHT: 1
TAGS: transparent
: if you.in_branch("Snake") then
KMONS: s = naga w:50 / naga warrior
: else
KMONS: s = centaur w:50 / centaur warrior
: end
KFEAT: s = armour shop type:Specialty suffix:Armours count:6 use_all ; \
   naga barding | naga barding | naga barding good_item | \
   centaur barding | centaur barding | centaur barding good_item
MAP
.......
..xxx..
.x...x.
.x.s.x.
.x...x.
..xxx..
.......
ENDMAP

#####################################################
# Sells conjurations-heavy books, destructive wands and rods, etc.
NAME: nicolae_blasting_shop
DEPTH: D:8-, Depths
TAGS: no_trap_gen no_monster_gen no_item_gen
MARKER: s = lua:fog_machine { cloud_type = "flame", \
             pow_min = 3, pow_max = 5, delay_min = 50, delay_max = 90, \
             start_clouds = 1, size = 3, walk_dist = 0, spread_rate= 0 }
KFEAT: s = general shop type:Demolitions suffix:Depot ; \
   book of conjurations | book of power | book of annihilations | \
   randbook disc:conjuration | book of the tempests | book of clouds | \
   wand of iceblast | wand of flame | wand of disintegration | \
   wand of lightning | staff of conjuration | lightning rod | \
   rod of inaccuracy | iron rod | \
   large rock ego:exploding | arrow ego:exploding | bolt ego:exploding | \
   javelin ego:exploding | wand of acid | \
   tomahawk ego:exploding | rod of ignition | fan of gales | disc of storms | \
   lamp of fire | phial of floods | stone of tremors | rod of clouds | \
   sling bullet ego:exploding | scroll of immolation | book of flames | \
   book of fire | book of air | manual of conjurations | manual of evocations
FTILE: ' = floor_rough_red
COLOUR: ' = red
MAP
  ...
 ..x..
..'''..
.x's'x.
..'''..
 ..x..
  ...
ENDMAP

#####################################################
# Contains only four items: an evocable item, a staff, a randbook, and a wand,
# associated with one of each of the four elemental schools.
NAME: nicolae_elemental_shop
TAGS: no_monster_gen no_trap_gen no_item_gen no_pool_fixup
DEPTH: D:4-, Depths
SHUFFLE: AB
NSUBST: A = a / l / w / x
SUBST: B = .
KPROP: l = no_cloud_gen
MARKER: a = lua:fog_machine { cloud_type = "thin mist", \
            pow_min = 100, pow_max = 100, delay = 1, \
            size = 1, walk_dist = 0, start_clouds = 1 }
{{
  local evocables = {"lamp of fire","phial of floods",
                     "fan of gales","stone of tremors"}
  local staffs    = {"staff of fire","staff of cold",
                     "staff of air","staff of earth"}
  local randbooks = {"randbook disc:fire","randbook disc:ice",
                     "randbook disc:air","randbook disc:earth"}
  local wands     = {"wand of flame","wand of iceblast",
                     "wand of lightning","wand of digging"}
  local choices   = {1, 2, 3, 4}
  local i, j, temp
  for i = 4, 2, -1 do
     j = crawl.random_range(1,i)
     temp = choices[j] ; choices[j] = choices[i] ; choices[i] = temp
  end
  local inventory = evocables[choices[1]] .. " | " .. staffs[choices[2]] ..
           " | " .. randbooks[choices[3]] .. " | " .. wands[choices[4]]
  kfeat("s = general shop type:Elemental suffix:Accessories count:4 use_all ; "
         .. inventory )
}}
MAP
.......
.B.A.B.
.......
.A.s.A.
.......
.B.A.B.
.......
ENDMAP

# Wands for cheap! (Charges not included.)
NAME: nicolae_for_you_no_charge
DEPTH: D:5-, Depths, Elf, Orc
TAGS: transparent
SHUFFLE: bv/bc/cv/bm/vm/cm
KFEAT: S = gadget shop type:Quality suffix:Used_Wands ; any wand charges:0
MAP
.......
..b.v..
.b...v.
...S...
.v...b.
..v.b..
.......
ENDMAP

# Sells only randbooks. All unique first editions with goofy names.
NAME: nicolae_rare_books
TAGS: transparent
DEPTH: Elf, Depths
KFEAT: S = book shop type:One-of-a-Kind suffix:Books count:5 ; \
       any fixed theme book | any fixed level book
MAP
 c.c
.....
c.S.c
.....
 c.c
ENDMAP
